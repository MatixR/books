C********************************************************************
      PROGRAM FLOWP
C********************************************************************
C
C     3D, Cartesian, laminar, multigrid
C     =================================
C
C           M. Peric, Z. Lilek, IfS, Hamburg, 1995
C********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /PARA/ GDS(NFI),URF(NFI),NSW(NFI),SOR(NFI),RESOR(NFI),
     *        SNOR(NFI),ALFA,SORMAX,SLARGE,FLOWIN,SMALL,GREAT
      COMMON /LOGIC/ LCAL(NFI),LTEST,LREAD,LWRITE,LSOL,LSOR,
     *         LOUTS,LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
      COMMON /GEO/ X(NXA),Y(NYA),Z(NZA),FX(NXA),FY(NYA),FZ(NZA)
      COMMON /VARF/ U(NXYZA),V(NXYZA),W(NXYZA),P(NXYZA),PP(NXYZA),
     *       T(NXYZA),F1(NXYZA),F2(NXYZA),F3(NXYZA),VIS(NXYZA),
     *       DEN(NXYZA)
      COMMON /VARG/ U1(NXYZM),V1(NXYZM),W1(NXYZM),
     *       T1(NXYZM),F1G(NXYZM),F2G(NXYZM),F3G(NXYZM),
     *       RESU(NXYZM),RESV(NXYZM),RESW(NXYZM),REST(NXYZM)
      COMMON /COEFF/ AE(NXYZA),AW(NXYZA),AN(NXYZA),AS(NXYZA),
     *         AB(NXYZA),AT(NXYZA),SU(NXYZA),APR(NXYZA),AP(NXYZA),
     *         SPV(NXYZA),SPW(NXYZA),SV(NXYZA),SW(NXYZA)
      COMMON /BC/ LBS(NXZA),LBN(NXZA),LBW(NYZA),LBE(NYZA),
     *            LBB(NXYA),LBT(NXYA)
      LOGICAL LCAL,LTEST,LREAD,LWRITE,LOUTS,LSOL,LSOR,
     *        LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
      CHARACTER*10 FILIN,FILOUT,FILGRD,FILRES
C
C-----INITIALISATION, GRID DEFINITION
C
      PRINT *, ' ENTER INPUT FILE NAME:  '
      READ(*,1) FILIN
      PRINT *, ' ENTER OUTPUT FILE NAME:  '
      READ(*,1) FILOUT
      PRINT *, ' ENTER GRID FILE NAME:  '
      READ(*,1) FILGRD
      PRINT *, ' ENTER RESULTS FILE NAME:  '
      READ(*,1) FILRES
   1  FORMAT(A10)
      OPEN (UNIT=6,FILE=FILOUT)
      OPEN (UNIT=5,FILE=FILIN)
      OPEN (UNIT=4,FILE=FILGRD)
      OPEN (UNIT=3,FILE=FILRES,FORM='UNFORMATTED')
      open(unit=11,file='uprof')
      open(unit=12,file='wprof')
      REWIND 6
      REWIND 5
      REWIND 4
      REWIND 3
      rewind 11
      rewind 12
C
      ITER=0
      CALL INIT
C
C------READ FIELD VALUES - RESULTS OF PREVIOUS RUN
C
c      IF(LREAD) READ(7) ITER,NI,NJ,NK,X,Y,Z,F1,F2,F3,U,V,W,P,T,VIS
c      REWIND 7
C
C-----INITIAL OUTPUT
C
      CALL MOOUT1
C.....
      DO 200 KGRID=1,NGIT
c
      IF(KGRID.GT.1) ITER=0
      IF(KGRID.GT.1) ITERF=0
      CALL SETIND(KGRID)
C
      IIM=2**(KGRID-1)*(IMON-1)+1
      JJM=2**(KGRID-1)*(JMON-1)+1
      KKM=2**(KGRID-1)*(KMON-1)+1
      IJKMON=LK(KKM)+LI(IIM)+JJM
C.....
      IF(KGRID.GT.1) THEN
C.....INTERPOLATE COARSE TO FINE
C
      IF(LCAL(IU))  CALL VINT(KGRID-1,U,U)
      IF(LCAL(IV))  CALL VINT(KGRID-1,V,V)
      IF(LCAL(IW))  CALL VINT(KGRID-1,W,W)
      IF(LCAL(IP))  CALL VINT(KGRID-1,P,P)
      IF(LCAL(IEN)) CALL VINT(KGRID-1,T,T)
C.....ESTIMATE NEW FLUX
      DO 150 K=2,NKM
      MK=LK(K)
      DZ=Z(K+KST)-Z(K+KST-1)
      DO 150 I=2,NIM-1
      MI=MK+LI(I)
      FXE=FX(I+IST)
      FXP=1.-FXE
      DO 150 J=2,NJM
      IJK=MI+J
      IJKE=IJK+NJ
      DY=Y(J+JST)-Y(J+JST-1)
C.....EAST CELL FACE
      ARE=DZ*DY
      DENE=DEN(IJKE)*FXE+DEN(IJK)*FXP
      UE=U(IJKE)*FXE+U(IJK)*FXP
      F1(IJK)=DENE*UE*ARE
  150 CONTINUE
C
      DO 155 K=2,NKM
      MK=LK(K)
      DZ=Z(K+KST)-Z(K+KST-1)
      DO 155 I=2,NIM
      MI=MK+LI(I)
      DX=X(I+IST)-X(I+IST-1)
      DO 155 J=2,NJM-1
      IJK=MI+J
      IJKN=IJK+1
      FYN=FY(J+JST)
      FYP=1.-FYN
C.....NORTH CELL FACE
      ARN=DZ*DX
      DENN=DEN(IJKN)*FYN+DEN(IJK)*FYP
      VN=V(IJKN)*FYN+V(IJK)*FYP
      F2(IJK)=DENN*VN*ARN
  155 CONTINUE
C.....
      DO 160 K=2,NKM-1
      MK=LK(K)
      FZT=FZ(K+KST)
      FZP=1.-FZT
      DO 160 I=2,NIM
      MI=MK+LI(I)
      DX=X(I+IST)-X(I+IST-1)
      DO 160 J=2,NJM
      IJK=MI+J
      IJKT=IJK+NIJ
      DY=Y(J+JST)-Y(J+JST-1)
C.....TOP CELL FACE
      ART=DX*DY
      DENT=DEN(IJKT)*FZT+DEN(IJK)*FZP
      WT=W(IJKT)*FZT+W(IJK)*FZP
      F3(IJK)=DENT*WT*ART
  160 CONTINUE
      ENDIF
C
C.....BOUNDARY CONDITIONS
      CALL BCIN
C
      IF(LOUTS) CALL OUTPUT
      WRITE(6,*) '  '
      WRITE(6,60) IIM,JJM,KKM
C.....
      MAXIT=MIT(KGRID)
      LSGIT=LSG(KGRID)
      ITERS=ITER+1
      ITERE=ITER+MAXIT
C
C.....ITERATION LOOP
      DO 100 ITER=ITERS,ITERE
C.....NUMBER OF ITEARATION ON FINE GRID
      LSOL=.TRUE.
      LSOR=.FALSE.
      DO 130 LS=1,LSGIT
      ITERF=ITERF+1
      IF(LCAL(IU))  CALL CALUVW(KGRID)
      IF(LCAL(IV))  CALL CALCP(KGRID)
C
C.....RESIDUAL NORMALIZATION, CONVERGENCE CHECK
      DO 90 I=1,NFI
   90 RESOR(I)=RESOR(I)*SNOR(I)
      WRITE(6,61) KGRID,ITER,LS,ITERF,(RESOR(I),I=1,NFI),U(IJKMON),
     *     V(IJKMON),W(IJKMON),P(IJKMON),T(IJKMON)
      SOURCE=AMAX1(RESOR(IU),RESOR(IV),RESOR(IW),RESOR(IP),RESOR(IEN))
      IF(SOURCE.GT.SLARGE) GO TO 102
      IF(SOURCE.LT.SORMAX) GO TO 103
  130 CONTINUE
C
C.....START SOLUTION ON ACTUAL V-CYCLE
C
      IF(KGRID.GT.1) THEN
      WRITE(6,*)
      DO 230 KKV=KGRID,2,-1
  230 CALL RESTR(KKV)
      WRITE(6,*)
C
      DO 240 KKV=1,KGRID-1
  240 CALL INTER(KKV)
      WRITE(6,*)
      ENDIF
C
  100 CONTINUE
      WRITE(6,64)
      GOTO 105
  103 CONTINUE
      WRITE(6,65)
  105 CONTINUE
C
      ITER=ITER-1
C
C-----PRINT RESULTS
C
      IF(LOUTE) THEN
      CALL OUTPUT
      CALL MOOUT2
      ENDIF
C
C-----WRITE FIELD VALUES FOR THE NEXT RUN
C
      IF(LWRITE) WRITE(3) ITER,NI,NJ,NK,NIJK,
     *    (X(I),I=IST+1,IST+NI),(Y(J),J=JST+1,JST+NJ),
     *    (Z(K),K=KST+1,KST+NK),
     *    (F1(IJK),IJK=IJKST+1,IJKST+NIJK),
     *    (F2(IJK),IJK=IJKST+1,IJKST+NIJK),
     *    (F3(IJK),IJK=IJKST+1,IJKST+NIJK),
     *    ( U(IJK),IJK=IJKST+1,IJKST+NIJK),
     *    ( V(IJK),IJK=IJKST+1,IJKST+NIJK),
     *    ( W(IJK),IJK=IJKST+1,IJKST+NIJK),
     *    ( P(IJK),IJK=IJKST+1,IJKST+NIJK),
     *    ( T(IJK),IJK=IJKST+1,IJKST+NIJK),
     *   (VIS(IJK),IJK=IJKST+1,IJKST+NIJK)
C     REWIND 3
c
  200 CONTINUE
      STOP
  102 WRITE(6,62)
      STOP
   60 FORMAT(1X,'GRID IT LSG ITF ',
     *  'I-----ABSOLUTE RESIDUAL SOURCE SUMS------I',2X,
     *  'I----------FIELD VALUES AT MONITORING LOCATION (',I2,
     *',',I2,',',I2,')----I',/,
     *    1X,'NO NO  NO  NO',2X,'UMOM',5X,'VMOM',5X,'WMOM',
     *5X,'MASS',5X,'ENER',8X,'U',9X,'V',9X,'W',9X,'P',9X,'T',/)
   61 FORMAT(1X,I2,1X,I2   ,1X,I3,1X,I3,1P5E9.2,1X,1P5E10.2)
   62 FORMAT(/,2X,'***PROGRAM TERMINATED - CONVERGENCE CRITERION  NOT
     *SATISFIED***',//)
   64 FORMAT(//,2X,'***CONVERGENCE CRITERION  NOT SATISFIED***',//)
   65 FORMAT(//,2X,'***CONVERGENCE CRITERION   IS SATISFIED***',//)
 6007 FORMAT(//,10X,'TIME SPENT FOR GRID ',I2,': ',1PE12.4,/,
     *      10X,36(1H*),/)
      END
C***********************************************************************
      SUBROUTINE RESTR(NGR)
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA),
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /PARA/ GDS(NFI),URF(NFI),NSW(NFI),SOR(NFI),RESOR(NFI),
     *        SNOR(NFI),ALFA,SORMAX,SLARGE,FLOWIN,SMALL,GREAT
      COMMON /LOGIC/ LCAL(NFI),LTEST,LREAD,LWRITE,LSOL,LSOR,
     *         LOUTS,LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
      COMMON /VARF/ U(NXYZA),V(NXYZA),W(NXYZA),P(NXYZA),PP(NXYZA),
     *       T(NXYZA),F1(NXYZA),F2(NXYZA),F3(NXYZA),VIS(NXYZA),
     *       DEN(NXYZA)
      COMMON /VARG/ U1(NXYZM),V1(NXYZM),W1(NXYZM),
     *       T1(NXYZM),F1G(NXYZM),F2G(NXYZM),F3G(NXYZM),
     *       RESU(NXYZM),RESV(NXYZM),RESW(NXYZM),REST(NXYZM)
      COMMON /COEFF/ AE(NXYZA),AW(NXYZA),AN(NXYZA),AS(NXYZA),
     *         AB(NXYZA),AT(NXYZA),SU(NXYZA),APR(NXYZA),AP(NXYZA),
     *         SPV(NXYZA),SPW(NXYZA),SV(NXYZA),SW(NXYZA)
      LOGICAL LCAL,LTEST,LREAD,LWRITE,LOUTS,LSOL,LSOR,
     *        LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
C
C.....SET INDEX FOR FINE GRID CALCULATIONS
      CALL SETIND(NGR)
C.....SET INDEX FOR COARSE GRID CALCULATIONS
      NIG=NIGIT(NGR-1)
      NJG=NJGIT(NGR-1)
      NKG=NKGIT(NGR-1)
      IJKSTG=IJKGIT(NGR-1)
      NIMG=NIG-1
      NJMG=NJG-1
      NKMG=NKG-1
      NIJG=NIG*NJG
C
C.....COARSE GRID
      DO 30 I=1,NIG
   30 LIG(I)=(I-1)*NJG+IJKSTG
      DO 40 K=1,NKG
   40 LKG(K)=(K-1)*NIJG
C.....CALCULATE ONLY NEW COEFICIENTS
      LSOL=.FALSE.
      IF(LCAL(IU)) THEN
      CALL CALUVW(NGR)
C.....CALCULATE RESU,RESV,RESW
      CALL RESFI(NGR,U,SU,AP ,RESU)
      CALL RESFI(NGR,V,SV,SPV,RESV)
      CALL RESFI(NGR,W,SW,SPW,RESW)
C.....
      DO 100 KG=2,NKMG
      KF=2*KG-1
      MKG=LKG(KG)
      MKF=LK(KF)
      DO 100 IG=2,NIMG
      IF=2*IG-1
      MIG=MKG+LIG(IG)
      MIF=MKF+LI(IF)
      DO 100 JG=2,NJMG
      JF=2*JG-1
      IJKG=MIG+JG
      IJKF=MIF+JF
      F1(IJKG)=F1(IJKF)+F1(IJKF-1)+F1(IJKF-NIJ)+F1(IJKF-NIJ-1)
      F2(IJKG)=F2(IJKF)+F2(IJKF-NJ)+F2(IJKF-NIJ)+F2(IJKF-NIJ-NJ)
      F3(IJKG)=F3(IJKF)+F3(IJKF-NJ)+F3(IJKF-1)+F3(IJKF-NJ-1)
      F1G(IJKG)=F1(IJKG)
      F2G(IJKG)=F2(IJKG)
      F3G(IJKG)=F3(IJKG)
  100 CONTINUE
C.....BOTTOM PLANE
      KG=1
      KF=2*KG-1
      MKG=LKG(KG)
      MKF=LK(KF)
      DO 110 IG=2,NIMG
      IF=2*IG-1
      MIG=MKG+LIG(IG)
      MIF=MKF+LI(IF)
      DO 110 JG=2,NJMG
      JF=2*JG-1
      IJKG=MIG+JG
      IJKF=MIF+JF
      F3(IJKG)=F3(IJKF)+F3(IJKF-NJ)+F3(IJKF-1)+F3(IJKF-NJ-1)
      F3G(IJKG)=F3(IJKG)
  110 CONTINUE
C.....WEST PLANE
      DO 120 KG=2,NKMG
      KF=2*KG-1
      MKG=LKG(KG)
      MKF=LK(KF)
      IG=1
      IF=2*IG-1
      MIG=MKG+LIG(IG)
      MIF=MKF+LI(IF)
      DO 120 JG=2,NJMG
      JF=2*JG-1
      IJKG=MIG+JG
      IJKF=MIF+JF
      F1(IJKG)=F1(IJKF)+F1(IJKF-1)+F1(IJKF-NIJ)+F1(IJKF-NIJ-1)
      F1G(IJKG)=F1(IJKG)
  120 CONTINUE
C.....SAUTH PLANE
      DO 130 KG=2,NKMG
      KF=2*KG-1
      MKG=LKG(KG)
      MKF=LK(KF)
      DO 130 IG=2,NIMG
      IF=2*IG-1
      MIG=MKG+LIG(IG)
      MIF=MKF+LI(IF)
      JG=1
      JF=2*JG-1
      IJKG=MIG+JG
      IJKF=MIF+JF
      F2(IJKG)=F2(IJKF)+F2(IJKF-NJ)+F2(IJKF-NIJ)+F2(IJKF-NIJ-NJ)
      F2G(IJKG)=F2(IJKG)
  130 CONTINUE
      ENDIF
C
C.....CALCULATE RESIDUALS FOR SCALARS
C.....ERROR  IT IS NOT FINISHED FOR SCALARS
      IF(LCAL(IEN)) THEN
      CALL CALCSC(NGR,IEN,T)
      CALL RESFI(NGR,T,SU,AP ,REST)
      ENDIF
C
C.....RESTRICT FINE GRID VALUES TO COARSE GRID
C
      IF(LCAL(IU))  CALL INTFG(NGR,U,U1)
      IF(LCAL(IV))  CALL INTFG(NGR,V,V1)
      IF(LCAL(IW))  CALL INTFG(NGR,W,W1)
      IF(LCAL(ITE)) CALL INTFG(NGR,T,T1)
C.....COARSE GRID BECOME FINE GRID
      CALL SETIND(NGR-1)
C.....SYMETRY BOUNDARY CONDITIONS FOR RESTICTED VARIABLES
      IF(LCAL(IU)) CALL SYMUVW
C     IF(LCAL(IEN) CALL SYMFI(IEN)
C.....
      DO 200 IJK=IJKST+1,IJKST+NIJK
      U(IJK)=U1(IJK)
      V(IJK)=V1(IJK)
      W(IJK)=W1(IJK)
      PP(IJK)=0.
      P(IJK)=0.
  200 CONTINUE
C
C.....SOLVE ON COARSE GRID (DO NS SWEEPS)
      LSOL=.TRUE.
      LSOR=.TRUE.
      NS=LSR(NGR-1)
C
      IIM=2**(NGR-2)*(IMON-1)+1
      JJM=2**(NGR-2)*(JMON-1)+1
      KKM=2**(NGR-2)*(KMON-1)+1
      IJKMON=LK(KKM)+LI(IIM)+JJM
C.....
      DO 250 LS=1,NS
C
C.....START RESTRICTION-CYCLE
      IF(LCAL(IU))  CALL CALUVW(NGR-1)
      IF(LCAL(IV))  CALL CALCP(NGR-1)
      IF(LCAL(IEN)) CALL CALCSC(NGR-1,IEN,T)
C
C-----RESIDUAL NORMALIZATION, CONVERGENCE CHECK
      DO 260 I=1,NFI
  260 RESOR(I)=RESOR(I)*SNOR(I)
      WRITE(6,61) NGR-1,LS,(RESOR(I),I=1,NFI),U(IJKMON),
     *            V(IJKMON),W(IJKMON),P(IJKMON),T(IJKMON)
      LSOR=.FALSE.
  250 CONTINUE
   61 FORMAT(1X,I2,1X,2H R  ,1X,I3,1X,3X,1P5E9.2,1X,1P5E10.2)
C  61 FORMAT(1X,I2,1X,I2    ,1X,I3,1X,I3,1P5E10.2,1X,1P5E10.2)
C  61 FORMAT(1X,I2,1X,1HR ,1X,   1X,1P5E10.2,1X,1P5E10.2)
      RETURN
      END
C***********************************************************************
      SUBROUTINE INTER(NGR)
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA),
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /PARA/ GDS(NFI),URF(NFI),NSW(NFI),SOR(NFI),RESOR(NFI),
     *        SNOR(NFI),ALFA,SORMAX,SLARGE,FLOWIN,SMALL,GREAT
      COMMON /LOGIC/ LCAL(NFI),LTEST,LREAD,LWRITE,LSOL,LSOR,
     *         LOUTS,LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
      COMMON /GEO/ X(NXA),Y(NYA),Z(NZA),FX(NXA),FY(NYA),FZ(NZA)
      COMMON /VARF/ U(NXYZA),V(NXYZA),W(NXYZA),P(NXYZA),PP(NXYZA),
     *       T(NXYZA),F1(NXYZA),F2(NXYZA),F3(NXYZA),VIS(NXYZA),
     *       DEN(NXYZA)
      COMMON /VARG/ U1(NXYZM),V1(NXYZM),W1(NXYZM),
     *       T1(NXYZM),F1G(NXYZM),F2G(NXYZM),F3G(NXYZM),
     *       RESU(NXYZM),RESV(NXYZM),RESW(NXYZM),REST(NXYZM)
      COMMON /COEFF/ AE(NXYZA),AW(NXYZA),AN(NXYZA),AS(NXYZA),
     *         AB(NXYZA),AT(NXYZA),SU(NXYZA),APR(NXYZA),AP(NXYZA),
     *         SPV(NXYZA),SPW(NXYZA),SV(NXYZA),SW(NXYZA)
      COMMON /BC/ LBS(NXZA),LBN(NXZA),LBW(NYZA),LBE(NYZA),
     *            LBB(NXYA),LBT(NXYA)
      LOGICAL LCAL,LTEST,LREAD,LWRITE,LOUTS,LSOL,LSOR,
     *        LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
C.....SET INDEX FOR FINE GRID CALCULATIONS
      CALL SETIND(NGR+1)
C.....SET INDEX FOR COARSE GRID CALCULATIONS
      NIG=NIGIT(NGR)
      NJG=NJGIT(NGR)
      NKG=NKGIT(NGR)
      IJKSTG=IJKGIT(NGR)
      NIMG=NIG-1
      NJMG=NJG-1
      NKMG=NKG-1
      NIJG=NIG*NJG
      NIJKG=NIG*NJG*NKG
C
C.....COARSE GRID
      DO 30 I=1,NIG
   30 LIG(I)=(I-1)*NJG+IJKSTG
      DO 40 K=1,NKG
   40 LKG(K)=(K-1)*NIJG
C
C.....INTERPOLATE VELOCITY CORRECTIONS
      IF(LCAL(IU)) THEN
C.....U-VELOCITY
      DO 50 IJK=IJKSTG+1,IJKSTG+NIJKG
   50 AP(IJK)=U(IJK)-U1(IJK)
      CALL VINT(NGR,AP,AP)
      DO 55 IJK=IJKST+1,IJKST+NIJK
   55 U(IJK)=U(IJK)+AP(IJK)
C.....V-VELOCITY
      DO 60 IJK=IJKSTG+1,IJKSTG+NIJKG
   60 SPV(IJK)=V(IJK)-V1(IJK)
      CALL VINT(NGR,SPV,SPV) 
      DO 65 IJK=IJKST+1,IJKST+NIJK  
   65 V(IJK)=V(IJK)+SPV(IJK) 
C.....W-VELOCITY
      DO 70 IJK=IJKSTG+1,IJKSTG+NIJKG
   70 SPW(IJK)=W(IJK)-W1(IJK) 
      CALL VINT(NGR,SPW,SPW) 
      DO 75 IJK=IJKST+1,IJKST+NIJK   
   75 W(IJK)=W(IJK)+SPW(IJK)  
C
C.....CALCULATE MASS FLUX CORRECTIONS
      CALL NEWFLX
      ENDIF
C
C.....INERPOLATE PRESSURE CORRECTIONS
      IF(LCAL(IP)) THEN
C.....PRESSURE 
      CALL VINT(NGR,P,AP)  
      DO 85 IJK=IJKST+1,IJKST+NIJK    
      P(IJK)=P(IJK)+AP(IJK)   
   85 CONTINUE
      ENDIF 
C
C.....INTERPOLATE SCALARS CORRECTIONS
      IF(LCAL(IEN)) THEN
C.....TEMPERATURE
      DO 90 IJK=IJKSTG+1,IJKSTG+NIJKG 
   90 AP(IJK)=T(IJK)-T1(IJK) 
      CALL VINT(NGR,AP,AP)  
      DO 95 IJK=IJKST+1,IJKST+NIJK    
   95 T(IJK)=T(IJK)+AP(IJK)   
      ENDIF 
C
C.....SOLVE ON FINE GRID (DO NS SWEEPS)
      LSOL=.TRUE.
      NS=LSI(NGR+1)
C
      IIM=2**(NGR)*(IMON-1)+1
      JJM=2**(NGR)*(JMON-1)+1
      KKM=2**(NGR)*(KMON-1)+1
      IJKMON=LK(KKM)+LI(IIM)+JJM
      DO 250 LS=1,NS
C
C.....START -CYCLE
      IF(LCAL(IU))  CALL CALUVW(NGR+1)
      IF(LCAL(IV))  CALL CALCP(NGR+1)
      IF(LCAL(IEN)) CALL CALCSC(NGR+1,IEN,T)
C
C.....RESIDUAL NORMALIZATION, CONVERGENCE CHECK
      DO 260 I=1,NFI
  260 RESOR(I)=RESOR(I)*SNOR(I)
C
C.....ADD NUMBER OF ITERATION ONLY IN FINE GRID
      IF(NGR+1.EQ.KGRID) THEN
      ITERF=ITERF+1
      WRITE(6,63) NGR+1,LS,ITERF,(RESOR(I),I=1,NFI),U(IJKMON),
     *     V(IJKMON),W(IJKMON),P(IJKMON),T(IJKMON)
      ELSE
      WRITE(6,61) NGR+1,LS,(RESOR(I),I=1,NFI),U(IJKMON),
     *            V(IJKMON),W(IJKMON),P(IJKMON),T(IJKMON)
      ENDIF
  250 CONTINUE
   61 FORMAT(1X,I2,1X,2H I  ,1X,I3,1X,3X,1P5E9.2,1X,1P5E10.2)
   63 FORMAT(1X,I2,1X,2H I  ,1X,I3,1X,I3,1P5E9.2,1X,1P5E10.2)
C  61 FORMAT(1X,I2,1X,1HI,1X,I3,1X,1P5E10.2,1X,1P5E10.2)
      RETURN
      END
C***********************************************************************
      SUBROUTINE NEWFLX
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /PARA/ GDS(NFI),URF(NFI),NSW(NFI),SOR(NFI),RESOR(NFI),
     *        SNOR(NFI),ALFA,SORMAX,SLARGE,FLOWIN,SMALL,GREAT
      COMMON /LOGIC/ LCAL(NFI),LTEST,LREAD,LWRITE,LSOL,LSOR,
     *         LOUTS,LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
      COMMON /GEO/ X(NXA),Y(NYA),Z(NZA),FX(NXA),FY(NYA),FZ(NZA)
      COMMON /VARF/ U(NXYZA),V(NXYZA),W(NXYZA),P(NXYZA),PP(NXYZA),
     *       T(NXYZA),F1(NXYZA),F2(NXYZA),F3(NXYZA),VIS(NXYZA),
     *       DEN(NXYZA)
      COMMON /VARG/ U1(NXYZM),V1(NXYZM),W1(NXYZM),
     *       T1(NXYZM),F1G(NXYZM),F2G(NXYZM),F3G(NXYZM),
     *       RESU(NXYZM),RESV(NXYZM),RESW(NXYZM),REST(NXYZM)
      COMMON /COEFF/ AE(NXYZA),AW(NXYZA),AN(NXYZA),AS(NXYZA),
     *         AB(NXYZA),AT(NXYZA),SU(NXYZA),APR(NXYZA),AP(NXYZA),
     *         SPV(NXYZA),SPW(NXYZA),SV(NXYZA),SW(NXYZA)
      COMMON /BC/ LBS(NXZA),LBN(NXZA),LBW(NYZA),LBE(NYZA),
     *            LBB(NXYA),LBT(NXYA)
      LOGICAL LCAL,LTEST,LREAD,LWRITE,LOUTS,LSOL,LSOR,
     *        LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
C
C.....CALCULATE MASS FLUX CORRECTIONS
C.....ESTIMATE NEW FLUX
      DO 150 K=2,NKM
      MK=LK(K)
      DZ=Z(K+KST)-Z(K+KST-1)
      DO 150 I=2,NIM-1
      MI=MK+LI(I)
      FXE=FX(I+IST)
      FXP=1.-FXE
      DO 150 J=2,NJM
      IJK=MI+J
      IJKE=IJK+NJ
      DY=Y(J+JST)-Y(J+JST-1)
C.....EAST CELL FACE
      ARE=DZ*DY
      DENE=DEN(IJKE)*FXE+DEN(IJK)*FXP
      UE=AP(IJKE)*FXE+AP(IJK)*FXP
      F1(IJK)=F1(IJK)+DENE*UE*ARE
  150 CONTINUE
C
      DO 155 K=2,NKM
      MK=LK(K)
      DZ=Z(K+KST)-Z(K+KST-1)
      DO 155 I=2,NIM
      MI=MK+LI(I)
      DX=X(I+IST)-X(I+IST-1)
      DO 155 J=2,NJM-1
      IJK=MI+J
      IJKN=IJK+1
      FYN=FY(J+JST)
      FYP=1.-FYN
C.....NORTH CELL FACE
      ARN=DZ*DX
      DENN=DEN(IJKN)*FYN+DEN(IJK)*FYP
      VN=SPV(IJKN)*FYN+SPV(IJK)*FYP
      F2(IJK)=F2(IJK)+DENN*VN*ARN
  155 CONTINUE
C.....
      DO 160 K=2,NKM-1
      MK=LK(K)
      FZT=FZ(K+KST)
      FZP=1.-FZT
      DO 160 I=2,NIM
      MI=MK+LI(I)
      DX=X(I+IST)-X(I+IST-1)
      DO 160 J=2,NJM
      IJK=MI+J
      IJKT=IJK+NIJ
      DY=Y(J+JST)-Y(J+JST-1)
C.....TOP CELL FACE
      ART=DX*DY
      DENT=DEN(IJKT)*FZT+DEN(IJK)*FZP
      WT=SPW(IJKT)*FZT+SPW(IJK)*FZP
      F3(IJK)=F3(IJK)+DENT*WT*ART
  160 CONTINUE
      RETURN
      END
C***********************************************************************
      SUBROUTINE SETIND(NGR)
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA),
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
C
      NI=NIGIT(NGR)
      NJ=NJGIT(NGR)
      NK=NKGIT(NGR)
      IST=IGIT(NGR)
      JST=JGIT(NGR)
      KST=KGIT(NGR)
      IJST=ISBIJ(NGR)
      IKST=ISBIK(NGR)
      JKST=ISBJK(NGR)
      IJKST=IJKGIT(NGR)
      NIM=NI-1
      NJM=NJ-1
      NKM=NK-1
      NIJ=NI*NJ
      NIK=NI*NK
      NJK=NJ*NK
      NIJK=NI*NJ*NK
      DO 10 I=1,NI
   10 LI(I)=(I-1)*NJ+IJKST
      DO 20 K=1,NK
   20 LK(K)=(K-1)*NIJ
      RETURN
      END
C***********************************************************************
      SUBROUTINE RESFI(NGR,FI,SUFI,SPFI,RFI)
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA),
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /COEFF/ AE(NXYZA),AW(NXYZA),AN(NXYZA),AS(NXYZA),
     *         AB(NXYZA),AT(NXYZA),SU(NXYZA),APR(NXYZA),AP(NXYZA),
     *         SPV(NXYZA),SPW(NXYZA),SV(NXYZA),SW(NXYZA)
      DIMENSION FI(NXYZA),SUFI(NXYZA),SPFI(NXYZA),RFI(NXYZM)
C
C.....CALCULATE RESIDUALS
      DO 10 K=2,NKM
      MK=LK(K)
      DO 10 I=2,NIM
      MI=MK+LI(I)
      INPS=MI+2
      INPE=MI+NJM
      DO 10 INP=INPS,INPE
      APTFI=AE(INP)+AW(INP)+AN(INP)+AS(INP)+AT(INP)+AB(INP)+SPFI(INP)
      INT=INP+NIJ
      INB=INP-NIJ
      INN=INP+1
      INS=INP-1
      INE=INP+NJ
      INW=INP-NJ
      SUFI(INP)=SUFI(INP)+
     *      AT(INP)*FI(INT)+AB(INP)*FI(INB)+
     *      AN(INP)*FI(INN)+AS(INP)*FI(INS)+
     *      AE(INP)*FI(INE)+AW(INP)*FI(INW)-APTFI*FI(INP)
   10 CONTINUE
C.....AD RESIDUALS ONLY ON COARSE GRID
      IF(NGR.NE.NGIT) THEN 
      DO 15 K=2,NKM
      MK=LK(K)
      DO 15 I=2,NIM
      MI=MK+LI(I)
      INPS=MI+2
      INPE=MI+NJM
      DO 15 INP=INPS,INPE
      SUFI(INP)=SUFI(INP)+RFI(INP)
   15 CONTINUE
      ENDIF
C
C.....TRANSFER RESIDULAS TO COARSE GRID
      NIMG=(NI-2)/2+1
      NJMG=(NJ-2)/2+1
      NKMG=(NK-2)/2+1
C
      DO 20 KG=2,NKMG
      KF=2*KG-2
      MKG=LKG(KG)
      MKF=LK(KF)
      DO 20 IG=2,NIMG
      IF=2*IG-2
      MIG=MKG+LIG(IG)
      MIF=MKF+LI(IF)
      DO 20 JG=2,NJMG
      JF=2*JG-2
      IJKG=MIG+JG
      IJKF=MIF+JF
C.....INDEX FINE
C.....BOTTOM
      IFSWB=IJKF
      IFSEB=IFSWB+NJ
      IFNWB=IFSWB+1
      IFNEB=IFSEB+1
C.....TOP
      IFSWT=IFSWB+NIJ
      IFSET=IFSWT+NJ
      IFNWT=IFSWT+1
      IFNET=IFSET+1
      RFI(IJKG)=SUFI(IFSWB)+SUFI(IFSEB)+SUFI(IFNEB)+SUFI(IFNWB)+
     *          SUFI(IFSWT)+SUFI(IFSET)+SUFI(IFNET)+SUFI(IFNWT)
   20 CONTINUE
      RETURN
      END
C***********************************************************************
      SUBROUTINE INTFG(NGR,FI,FIG)
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /GEO/ X(NXA),Y(NYA),Z(NZA),FX(NXA),FY(NYA),FZ(NZA)
      DIMENSION FI(NXYZA),FIG(NXYZM)
C
C.....SET INDEX FOR FINE GRID
      CALL SETIND(NGR)
C.....SET INDEX FOR COARSE GRID 
      NIG=NIGIT(NGR-1)
      NJG=NJGIT(NGR-1)
      NKG=NKGIT(NGR-1)
      IJKSTG=IJKGIT(NGR-1)
      NIMG=NIG-1
      NJMG=NJG-1
      NKMG=NKG-1
      NIJG=NIG*NJG
C
C.....COARSE GRID
      DO 30 I=1,NIG
   30 LIG(I)=(I-1)*NJG+IJKSTG
      DO 40 K=1,NKG
   40 LKG(K)=(K-1)*NIJG
C
C.....
      DO 50 KG=2,NKMG
      KF=2*KG-2
      MKG=LKG(KG)
      MKF=LK(KF)
      DO 50 IG=2,NIMG
      IF=2*IG-2
      MIG=MKG+LIG(IG)
      MIF=MKF+LI(IF)
      DO 50 JG=2,NJMG
      JF=2*JG-2
      IJKG=MIG+JG
      IJKF=MIF+JF
C.....INDEX FINE
C.....BOTTOM
      IFSWB=IJKF
      IFSEB=IFSWB+NJ
      IFNWB=IFSWB+1
      IFNEB=IFSEB+1
C.....TOP
      IFSWT=IFSWB+NIJ
      IFSET=IFSWT+NJ
      IFNWT=IFSWT+1
      IFNET=IFSET+1
C
      FISB=FI(IFSWB)*FX(IF+IST)+FI(IFSEB)*(1.-FX(IF+IST))
      FINB=FI(IFNWB)*FX(IF+IST)+FI(IFNEB)*(1.-FX(IF+IST))
      FIST=FI(IFSWT)*FX(IF+IST)+FI(IFSET)*(1.-FX(IF+IST))
      FINT=FI(IFNWT)*FX(IF+IST)+FI(IFNET)*(1.-FX(IF+IST))
      FIB=FISB*FY(JF+JST)+FINB*(1.-FY(JF+JST))
      FIT=FIST*FY(JF+JST)+FINT*(1.-FY(JF+JST))
      FIG(IJKG)=FIB*FZ(KF+KST)+FIT*(1.-FZ(KF+KST))
   50 CONTINUE
C.....BOTTOM PLANE
      KG=1
      KF=1
      MKG=LKG(KG)
      MKF=LK(KF)
      DO 60 IG=2,NIMG
      IF=2*IG-2
      MIG=MKG+LIG(IG)
      MIF=MKF+LI(IF)
      DO 60 JG=2,NJMG
      JF=2*JG-2
      IJKG=MIG+JG
      IJKF=MIF+JF
C.....INDEX FINE
      IFSWB=IJKF
      IFSEB=IFSWB+NJ
      IFNWB=IFSWB+1
      IFNEB=IFSEB+1
      FISB=FI(IFSWB)*FX(IF+IST)+FI(IFSEB)*(1.-FX(IF+IST))
      FINB=FI(IFNWB)*FX(IF+IST)+FI(IFNEB)*(1.-FX(IF+IST))
      FIG(IJKG)=FISB*FY(JF+JST)+FINB*(1.-FY(JF+JST)) 
   60 CONTINUE
C.....TOP PLANE
      KG=NKG
      KF=NK
      MKG=LKG(KG)
      MKF=LK(KF)
      DO 65 IG=2,NIMG
      IF=2*IG-2
      MIG=MKG+LIG(IG)
      MIF=MKF+LI(IF)
      DO 65 JG=2,NJMG
      JF=2*JG-2
      IJKG=MIG+JG
      IJKF=MIF+JF
C.....INDEX FINE
      IFSWB=IJKF
      IFSEB=IFSWB+NJ
      IFNWB=IFSWB+1
      IFNEB=IFSEB+1
      FISB=FI(IFSWB)*FX(IF+IST)+FI(IFSEB)*(1.-FX(IF+IST)) 
      FINB=FI(IFNWB)*FX(IF+IST)+FI(IFNEB)*(1.-FX(IF+IST)) 
      FIG(IJKG)=FISB*FY(JF+JST)+FINB*(1.-FY(JF+JST))  
   65 CONTINUE
C.....WEST PLANE
      IG=1
      IF=1
      DO 70 KG=2,NKMG
      KF=2*KG-2  
      MKG=LKG(KG)
      MKF=LK(KF)
      MIG=MKG+LIG(IG)
      MIF=MKF+LI(IF)
      DO 70 JG=2,NJMG
      JF=2*JG-2  
      IJKG=MIG+JG
      IJKF=MIF+JF
C.....INDEX FINE
      IFSWB=IJKF
      IFNWB=IFSWB+1
      IFSWT=IFSWB+NIJ
      IFNWT=IFSWT+1
      FIB=FI(IFSWB)*FY(JF+JST)+FI(IFNWB)*(1.-FY(JF+JST))
      FIT=FI(IFSWT)*FY(JF+JST)+FI(IFNWT)*(1.-FY(JF+JST))
      FIG(IJKG)=FIB*FZ(KF+KST)+FIT*(1.-FZ(KF+KST))
   70 CONTINUE
C.....EAST PLANE
      IG=NIG
      IF=NI
      DO 75 KG=2,NKMG
      KF=2*KG-2  
      MKG=LKG(KG)
      MKF=LK(KF)
      MIG=MKG+LIG(IG)
      MIF=MKF+LI(IF)
      DO 75 JG=2,NJMG
      JF=2*JG-2  
      IJKG=MIG+JG
      IJKF=MIF+JF
C.....INDEX FINE
      IFSWB=IJKF
      IFNWB=IFSWB+1
      IFSWT=IFSWB+NIJ
      IFNWT=IFSWT+1
      FIB=FI(IFSWB)*FY(JF+JST)+FI(IFNWB)*(1.-FY(JF+JST))
      FIT=FI(IFSWT)*FY(JF+JST)+FI(IFNWT)*(1.-FY(JF+JST))
      FIG(IJKG)=FIB*FZ(KF+KST)+FIT*(1.-FZ(KF+KST))
   75 CONTINUE
C.....SOUTH PLANE
      JG=1
      JF=1
      DO 80 KG=2,NKMG
      KF=2*KG-2
      MKG=LKG(KG)
      MKF=LK(KF)
      DO 80 IG=2,NIMG
      IF=2*IG-2
      MIG=MKG+LIG(IG)
      MIF=MKF+LI(IF)
      IJKG=MIG+JG
      IJKF=MIF+JF
C.....INDEX FINE
      IFSWB=IJKF
      IFSEB=IFSWB+NJ
      IFSWT=IFSWB+NIJ
      IFSET=IFSWT+NJ
      FISB=FI(IFSWB)*FX(IF+IST)+FI(IFSEB)*(1.-FX(IF+IST))
      FIST=FI(IFSWT)*FX(IF+IST)+FI(IFSET)*(1.-FX(IF+IST))
      FIG(IJKG)=FISB*FZ(KF+KST)+FIST*(1.-FZ(KF+KST))
   80 CONTINUE
C.....NORTH PLANE
      JG=NJG
      JF=NJ
      DO 85 KG=2,NKMG
      KF=2*KG-2
      MKG=LKG(KG)
      MKF=LK(KF)
      DO 85 IG=2,NIMG
      IF=2*IG-2
      MIG=MKG+LIG(IG)
      MIF=MKF+LI(IF)
      IJKG=MIG+JG  
      IJKF=MIF+JF
C.....INDEX FINE
      IFSWB=IJKF
      IFSEB=IFSWB+NJ
      IFSWT=IFSWB+NIJ
      IFSET=IFSWT+NJ
      FISB=FI(IFSWB)*FX(IF+IST)+FI(IFSEB)*(1.-FX(IF+IST))
      FIST=FI(IFSWT)*FX(IF+IST)+FI(IFSET)*(1.-FX(IF+IST))
      FIG(IJKG)=FISB*FZ(KF+KST)+FIST*(1.-FZ(KF+KST))
   85 CONTINUE
C
C.....BOUNDARY LINE
C.....IN X-WAY
      DO 90 IG=2,NIMG
      IF=2*IG-2
C
      IJKG=LKG(1)+LIG(IG)+1
      IJKF=LK(1)+LI(IF)+1
      FIG(IJKG)=FI(IJKF)*FX(IF+IST)+FI(IJKF+NJ)*(1.-FX(IF+IST))
      IJKG=LKG(1)+LIG(IG)+NJG
      IJKF=LK(1)+LI(IF)+NJ
      FIG(IJKG)=FI(IJKF)*FX(IF+IST)+FI(IJKF+NJ)*(1.-FX(IF+IST))
C
      IJKG=LKG(NKG)+LIG(IG)+1
      IJKF=LK(NK)+LI(IF)+1
      FIG(IJKG)=FI(IJKF)*FX(IF+IST)+FI(IJKF+NJ)*(1.-FX(IF+IST)) 
      IJKG=LKG(NKG)+LIG(IG)+NJG
      IJKF=LK(NK)+LI(IF)+NJ
      FIG(IJKG)=FI(IJKF)*FX(IF+IST)+FI(IJKF+NJ)*(1.-FX(IF+IST))
   90 CONTINUE 
C.....IN Y-WAY
      DO 100 JG=2,NJMG
      JF=2*JG-2
C
      IJKG=LKG(1)+LIG(1)+JG
      IJKF=LK(1)+LI(1)+JF
      FIG(IJKG)=FI(IJKF)*FY(JF+JST)+FI(IJKF+1)*(1.-FY(JF+JST)) 
      IJKG=LKG(1)+LIG(NIG)+JG
      IJKF=LK(1)+LI(NI)+JF
      FIG(IJKG)=FI(IJKF)*FY(JF+JST)+FI(IJKF+1)*(1.-FY(JF+JST)) 
C
      IJKG=LKG(NKG)+LIG(1)+JG
      IJKF=LK(NK)+LI(1)+JF 
      FIG(IJKG)=FI(IJKF)*FY(JF+JST)+FI(IJKF+1)*(1.-FY(JF+JST))
      IJKG=LKG(NKG)+LIG(NIG)+JG
      IJKF=LK(NK)+LI(NI)+JF
      FIG(IJKG)=FI(IJKF)*FY(JF+JST)+FI(IJKF+1)*(1.-FY(JF+JST))
  100 CONTINUE
C.....IN Z-WAY
      DO 110 KG=2,NKMG
      KF=2*KG-2
C
      IJKG=LKG(KG)+LIG(1)+1
      IJKF=LK(KF)+LI(1)+1 
      FIG(IJKG)=FI(IJKF)*FZ(KF+KST)+FI(IJKF+NIJ)*(1.-FZ(KF+KST))
      IJKG=LKG(KG)+LIG(NIG)+1
      IJKF=LK(KF)+LI(NI)+1  
      FIG(IJKG)=FI(IJKF)*FZ(KF+KST)+FI(IJKF+NIJ)*(1.-FZ(KF+KST)) 
C
      IJKG=LKG(KG)+LIG(1)+NJG
      IJKF=LK(KF)+LI(1)+NJ  
      FIG(IJKG)=FI(IJKF)*FZ(KF+KST)+FI(IJKF+NIJ)*(1.-FZ(KF+KST)) 
      IJKG=LKG(KG)+LIG(NIG)+NJG
      IJKF=LK(KF)+LI(NI)+NJ   
      FIG(IJKG)=FI(IJKF)*FZ(KF+KST)+FI(IJKF+NIJ)*(1.-FZ(KF+KST)) 
  110 CONTINUE
C
C.....CORNERS
C.....BOTTOM
      IJKG=LKG(1)+LIG(1)+1
      IJKF=LK(1)+LI(1)+1
      FIG(IJKG)=FI(IJKF)
C
      IJKG=LKG(1)+LIG(1)+NJG
      IJKF=LK(1)+LI(1)+NJ 
      FIG(IJKG)=FI(IJKF) 
C
      IJKG=LKG(1)+LIG(NIG)+NJG
      IJKF=LK(1)+LI(NI)+NJ   
      FIG(IJKG)=FI(IJKF)  
C 
      IJKG=LKG(1)+LIG(NIG)+1
      IJKF=LK(1)+LI(NI)+1  
      FIG(IJKG)=FI(IJKF) 
C.....TOP
      IJKG=LKG(NKG)+LIG(1)+1
      IJKF=LK(NK)+LI(1)+1 
      FIG(IJKG)=FI(IJKF) 
C
      IJKG=LKG(NKG)+LIG(1)+NJG
      IJKF=LK(NK)+LI(1)+NJ   
      FIG(IJKG)=FI(IJKF)  
C 
      IJKG=LKG(NKG)+LIG(NIG)+NJG 
      IJKF=LK(NK)+LI(NI)+NJ   
      FIG(IJKG)=FI(IJKF) 

      IJKG=LKG(NKG)+LIG(NIG)+1
      IJKF=LK(NK)+LI(NI)+1
      FIG(IJKG)=FI(IJKF)
C.....ENDE
      RETURN
      END
C***********************************************************************
      SUBROUTINE CALUVW(NGR)
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /PARA/ GDS(NFI),URF(NFI),NSW(NFI),SOR(NFI),RESOR(NFI),
     *        SNOR(NFI),ALFA,SORMAX,SLARGE,FLOWIN,SMALL,GREAT
      COMMON /LOGIC/ LCAL(NFI),LTEST,LREAD,LWRITE,LSOL,LSOR,
     *         LOUTS,LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
      COMMON /GEO/ X(NXA),Y(NYA),Z(NZA),FX(NXA),FY(NYA),FZ(NZA)
      COMMON /VARF/ U(NXYZA),V(NXYZA),W(NXYZA),P(NXYZA),PP(NXYZA),
     *       T(NXYZA),F1(NXYZA),F2(NXYZA),F3(NXYZA),VIS(NXYZA),
     *       DEN(NXYZA)
      COMMON /VARG/ U1(NXYZM),V1(NXYZM),W1(NXYZM),
     *       T1(NXYZM),F1G(NXYZM),F2G(NXYZM),F3G(NXYZM),
     *       RESU(NXYZM),RESV(NXYZM),RESW(NXYZM),REST(NXYZM)
      COMMON /COEFF/ AE(NXYZA),AW(NXYZA),AN(NXYZA),AS(NXYZA),
     *         AB(NXYZA),AT(NXYZA),SU(NXYZA),APR(NXYZA),AP(NXYZA),
     *         SPV(NXYZA),SPW(NXYZA),SV(NXYZA),SW(NXYZA)
      COMMON /BC/ LBS(NXZA),LBN(NXZA),LBW(NYZA),LBE(NYZA),
     *            LBB(NXYA),LBT(NXYA)
      LOGICAL LCAL,LTEST,LREAD,LWRITE,LOUTS,LSOL,LSOR,
     *        LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
C
      GAM=GDS(IU)
C
C.....INITIALISATION
      DO 10 K=2,NKM
      MK=LK(K)
      DO 10 I=2,NIM
      MI=MK+LI(I)
      DO 10 IJK=MI+2,MI+NJM
      SU(IJK)=0.
      SV(IJK)=0.
   10 SW(IJK)=0.
C
C.....INNER CELL FACES
      DO 100 K=2,NKM
      MK=LK(K)
      DZ=Z(K+KST)-Z(K+KST-1)
      DZTR=2./(Z(K+KST+1)-Z(K+KST-1))
      FZT=FZ(K+KST)
      FZP=1.-FZT
C
      DO 100 I=2,NIM
      MI=MK+LI(I)
      DX=X(I+IST)-X(I+IST-1)
      DXER=2./(X(I+IST+1)-X(I+IST-1))
      FXE=FX(I+IST)
      FXP=1.-FXE
C
      DO 100 IJK=MI+2,MI+NJM
      J=IJK-MI
      IJKE=IJK+NJ
      IJKN=IJK+1
      IJKT=IJK+NIJ
      DY=Y(J+JST)-Y(J+JST-1)
      DYNR=2./(Y(J+JST+1)-Y(J+JST-1))
      FYN=FY(J+JST)
      FYP=1.-FYN
C
C.....CELL FACE FLUX APPROXIMATIONS
C
C.....EAST CELL FACE
      PE=0.
      IF(I.NE.NIM) THEN
      ARE=DY*DZ
      DE=(VIS(IJKE)*FXE+VIS(IJK)*FXP)*DY*DZ*DXER
      PE=(P(IJKE)*FXE+P(IJK)*FXP)*DY*DZ
C
      CF=F1(IJK)
      CE=AMAX1(-CF,0.)
      CW=AMAX1( CF,0.)
C     
C.....UDS COEFFICIENTS
      AE(IJK)= DE+CE
      AW(IJKE)=DE+CW
C.....HIGH (H) AND LOW (L) ORDER CONVECTIVE TERMS
      UHE=CF*(U(IJKE)*FXE+U(IJK)*FXP)
      VHE=CF*(V(IJKE)*FXE+V(IJK)*FXP)
      WHE=CF*(W(IJKE)*FXE+W(IJK)*FXP)
C
      ULE=CW*U(IJK)-CE*U(IJKE)
      VLE=CW*V(IJK)-CE*V(IJKE)
      WLE=CW*W(IJK)-CE*W(IJKE)
C
      SUC=-GAM*(UHE-ULE)
      SVC=-GAM*(VHE-VLE)
      SWC=-GAM*(WHE-WLE) 
C
      SU(IJK)=SU(IJK) +SUC-PE
      SV(IJK)=SV(IJK) +SVC
      SW(IJK)=SW(IJK) +SWC

      SU(IJKE)=SU(IJKE) -SUC+PE
      SV(IJKE)=SV(IJKE) -SVC
      SW(IJKE)=SW(IJKE) -SWC
C
      ENDIF
C
C.....NORTH CELL FACE
      PN=0.
      IF(J.NE.NJM) THEN
      ARN=DX*DZ
      DN=(VIS(IJKN)*FYN+VIS(IJK)*FYP)*ARN*DYNR
      PN=(P(IJKN)*FYN+P(IJK)*FYP)*ARN
C
      CF=F2(IJK)
      CN=AMAX1(-CF,0.)
      CS=AMAX1( CF,0.)
C     
C.....UDS COEFFICIENTS
      AN(IJK)= DN+CN
      AS(IJKN)=DN+CS
C.....HIGH (H) AND LOW (L) ORDER CONVECTIVE TERMS
      UHN=CF*(U(IJKN)*FYN+U(IJK)*FYP)
      VHN=CF*(V(IJKN)*FYN+V(IJK)*FYP)
      WHN=CF*(W(IJKN)*FYN+W(IJK)*FYP)
C
      ULN=CS*U(IJK)-CN*U(IJKN)
      VLN=CS*V(IJK)-CN*V(IJKN)
      WLN=CS*W(IJK)-CN*W(IJKN)
C
      SUC=-GAM*(UHN-ULN)
      SVC=-GAM*(VHN-VLN)
      SWC=-GAM*(WHN-WLN)
C
      SU(IJK)=SU(IJK)   +SUC
      SV(IJK)=SV(IJK)   +SVC -PN
      SW(IJK)=SW(IJK)   +SWC
      SU(IJKN)=SU(IJKN) -SUC
      SV(IJKN)=SV(IJKN) -SVC +PN
      SW(IJKN)=SW(IJKN) -SWC
C
      ENDIF
C
C.....TOP CELL FACE
      PT=0.
      IF(K.NE.NKM) THEN
      ART=DX*DY
      DT=(VIS(IJKT)*FZT+VIS(IJK)*FZP)*ART*DZTR
      PT=(P(IJKT)*FZT+P(IJK)*FZP)*ART
C
      CF=F3(IJK)
      CT=AMAX1(-CF,0.)
      CB=AMAX1( CF,0.)
C     
C.....UDS COEFFICIENTS
      AT(IJK)= DT+CT
      AB(IJKT)=DT+CB
C.....HIGH (H) AND LOW (L) ORDER CONVECTIVE TERMS
      UHT=CF*(U(IJKT)*FZT+U(IJK)*FZP)
      VHT=CF*(V(IJKT)*FZT+V(IJK)*FZP)
      WHT=CF*(W(IJKT)*FZT+W(IJK)*FZP)
C
      ULT=CB*U(IJK)-CT*U(IJKT)
      VLT=CB*V(IJK)-CT*V(IJKT)
      WLT=CB*W(IJK)-CT*W(IJKT)
C
      SUC=-GAM*(UHT-ULT)
      SVC=-GAM*(VHT-VLT)
      SWC=-GAM*(WHT-WLT)
C
      SU(IJK)=SU(IJK) +SUC
      SV(IJK)=SV(IJK) +SVC
      SW(IJK)=SW(IJK) +SWC -PT
C
      SU(IJKT)=SU(IJKT) -SUC
      SV(IJKT)=SV(IJKT) -SVC
      SW(IJKT)=SW(IJKT) -SWC +PT
C
      ENDIF
C
C.....SUM OF SOURCES
      SPV(IJK)=0.
      SPW(IJK)=0.
      AP(IJK)=0.
 100  CONTINUE
C
C.....IMPLEMENT BOUNDARY CONDITIONS
      CALL BCUVW
      IF(.NOT.LSOL) RETURN
C.....ADDITIONAL SOURCES FOR FAS
      IF(LSOR) THEN
      DO 200 K=2,NKM
      MK=LK(K)
      DO 200 I=2,NIM
      MI=MK+LI(I)
      INPS=MI+2
      INPE=MI+NJM
      DO 200 INP=INPS,INPE
      APTU=AE(INP)+AW(INP)+AN(INP)+AS(INP)+AT(INP)+AB(INP)+AP(INP)
      APTV=AE(INP)+AW(INP)+AN(INP)+AS(INP)+AT(INP)+AB(INP)+SPV(INP)
      APTW=AE(INP)+AW(INP)+AN(INP)+AS(INP)+AT(INP)+AB(INP)+SPW(INP)
      INT=INP+NIJ
      INB=INP-NIJ
      INN=INP+1
      INS=INP-1
      INE=INP+NJ
      INW=INP-NJ
      SUC=AT(INP)*U1(INT)+AB(INP)*U1(INB)+
     *    AN(INP)*U1(INN)+AS(INP)*U1(INS)+
     *    AE(INP)*U1(INE)+AW(INP)*U1(INW)-APTU*U1(INP)
      SVC=AT(INP)*V1(INT)+AB(INP)*V1(INB)+
     *    AN(INP)*V1(INN)+AS(INP)*V1(INS)+
     *    AE(INP)*V1(INE)+AW(INP)*V1(INW)-APTV*V1(INP)
      SWC=AT(INP)*W1(INT)+AB(INP)*W1(INB)+
     *    AN(INP)*W1(INN)+AS(INP)*W1(INS)+
     *    AE(INP)*W1(INE)+AW(INP)*W1(INW)-APTW*W1(INP)
      RESU(INP)=RESU(INP)-SUC-SU(INP)
      RESV(INP)=RESV(INP)-SVC-SV(INP)
      RESW(INP)=RESW(INP)-SWC-SW(INP)
  200 CONTINUE
      ENDIF
C
C.....FINAL ASSEMBLY, UNDERRELAXATION, SOLUTION
C
C.....U-EQUATION
      URFR=1./URF(IU)
      URFM=1.-URF(IU)
      DO 450 K=2,NKM
      MK=LK(K)
      DO 450 I=2,NIM
      MI=MK+LI(I)
      DO 450 IJK=MI+2,MI+NJM
      APP=AE(IJK)+AW(IJK)+AN(IJK)+AS(IJK)+AT(IJK)+AB(IJK)+AP(IJK)
      AP(IJK)=APP*URFR
      APR(IJK)=1./AP(IJK)
      SU(IJK)=SU(IJK)+URFM*AP(IJK)*U(IJK)
  450 CONTINUE
C
C.....AD ONLY ON COARSE GRID
      IF(NGR.NE.NGIT) THEN
      DO 455 K=2,NKM
      MK=LK(K)
      DO 455 I=2,NIM
      MI=MK+LI(I)
      DO 455 IJK=MI+2,MI+NJM
  455 SU(IJK)=SU(IJK)+RESU(IJK)
      ENDIF
      CALL SIPSOL(U,IU)
C
C.....V-EQUATION
      URFR=1./URF(IV)
      URFM=1.-URF(IV)
      DO 500 K=2,NKM
      MK=LK(K)
      DO 500 I=2,NIM
      MI=MK+LI(I)
      DO 500 IJK=MI+2,MI+NJM
      APP=AE(IJK)+AW(IJK)+AN(IJK)+AS(IJK)+AT(IJK)+AB(IJK)+SPV(IJK)
      AP(IJK)=APP*URFR
      SPV(IJK)=1./AP(IJK)
      SU(IJK)=SV(IJK)+URFM*AP(IJK)*V(IJK)
  500 CONTINUE
C
C.....AD ONLY ON COARSE GRID
      IF(NGR.NE.NGIT) THEN
      DO 505 K=2,NKM
      MK=LK(K)
      DO 505 I=2,NIM
      MI=MK+LI(I)
      DO 505 IJK=MI+2,MI+NJM
  505 SU(IJK)=SU(IJK)+RESV(IJK)
      ENDIF
      CALL SIPSOL(V,IV)
C
C.....W-EQUATION
      URFR=1./URF(IW)
      URFM=1.-URF(IW)
      DO 550 K=2,NKM
      MK=LK(K)
      DO 550 I=2,NIM
      MI=MK+LI(I)
      DO 550 IJK=MI+2,MI+NJM
      APP=AE(IJK)+AW(IJK)+AN(IJK)+AS(IJK)+AT(IJK)+AB(IJK)+SPW(IJK)
      AP(IJK)=APP*URFR
      SPW(IJK)=1./AP(IJK)
      SU(IJK)=SW(IJK)+URFM*AP(IJK)*W(IJK)
  550 CONTINUE
C
C.....AD ONLY ON COARSE GRID
      IF(NGR.NE.NGIT) THEN 
      DO 555 K=2,NKM 
      MK=LK(K) 
      DO 555 I=2,NIM 
      MI=MK+LI(I) 
      DO 555 IJK=MI+2,MI+NJM 
  555 SU(IJK)=SU(IJK)+RESW(IJK) 
      ENDIF
      CALL SIPSOL(W,IW)
      RETURN
      END
C
C***********************************************************************
      SUBROUTINE CALCSC(NGR,IFI,FI)
C***********************************************************************
      RETURN
      END
C
C***********************************************************************
      SUBROUTINE CALCP(NGR)
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /PARA/ GDS(NFI),URF(NFI),NSW(NFI),SOR(NFI),RESOR(NFI),
     *        SNOR(NFI),ALFA,SORMAX,SLARGE,FLOWIN,SMALL,GREAT
      COMMON /LOGIC/ LCAL(NFI),LTEST,LREAD,LWRITE,LSOL,LSOR,
     *         LOUTS,LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
      COMMON /GEO/ X(NXA),Y(NYA),Z(NZA),FX(NXA),FY(NYA),FZ(NZA)
      COMMON /VARF/ U(NXYZA),V(NXYZA),W(NXYZA),P(NXYZA),PP(NXYZA),
     *       T(NXYZA),F1(NXYZA),F2(NXYZA),F3(NXYZA),VIS(NXYZA),
     *       DEN(NXYZA)
      COMMON /VARG/ U1(NXYZM),V1(NXYZM),W1(NXYZM),
     *       T1(NXYZM),F1G(NXYZM),F2G(NXYZM),F3G(NXYZM),
     *       RESU(NXYZM),RESV(NXYZM),RESW(NXYZM),REST(NXYZM)
      COMMON /COEFF/ AE(NXYZA),AW(NXYZA),AN(NXYZA),AS(NXYZA),
     *         AB(NXYZA),AT(NXYZA),SU(NXYZA),APR(NXYZA),AP(NXYZA),
     *         SPV(NXYZA),SPW(NXYZA),SV(NXYZA),SW(NXYZA)
      LOGICAL LCAL,LTEST,LREAD,LWRITE,LOUTS,LSOL,LSOR,
     *        LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
C
C.....CALCULATE PRESSURE 
      CALL CALDP
      CALL OUTBC
C
C.....CELL FACE VELOCITIES, MASS FLUXES
      COL=1.
      DO 200 K=2,NKM
      MK=LK(K)
      DZ=Z(K+KST)-Z(K+KST-1)
      FZT=FZ(K+KST)
      FZP=1.-FZT
      DO 200 I=2,NIM
      MI=MK+LI(I)
      DX=X(I+IST)-X(I+IST-1)
      FXE=FX(I+IST)
      FXP=1.-FXE
      DO 200 IJK=MI+2,MI+NJM
      IJKE=IJK+NJ
      IJKN=IJK+1
      IJKT=IJK+NIJ
      J=IJK-MI
      DY=Y(J+JST)-Y(J+JST-1)
      FYN=FY(J+JST)
      FYP=1.-FYN
C
C.....EAST CELL FACE
      IF(I.NE.NIM) THEN
      ARE=DZ*DY
      APER=APR(IJKE)*FXE+APR(IJK)*FXP
      DE=ARE*APER
      PEWI=SU(IJKE)*APR(IJKE)*ARE*FXE+
     *     SU(IJK)*APR(IJK)*ARE*FXP
      UE=U(IJKE)*FXE+U(IJK)*FXP
     *   +COL*(PEWI-DE*(P(IJKE)-P(IJK)))
      IF(NGR.NE.NGIT) THEN
      UE1=U1(IJKE)*FXE+U1(IJK)*FXP
      UE=UE-UE1
      ENDIF
      DENE=DEN(IJKE)*FXE+DEN(IJK)*FXP
      F1(IJK)=DENE*UE*ARE
      IF(NGR.NE.NGIT) F1(IJK)=F1(IJK)+F1G(IJK)
      AE(IJK)=DE*DENE*ARE
      AW(IJKE)=AE(IJK)
      ENDIF
C
C.....NORTH CELL FACE
      IF(J.NE.NJM) THEN
      ARE=DZ*DX
      APER=SPV(IJKN)*FYN+SPV(IJK)*FYP
      DN=ARE*APER
      PNSI=SV(IJKN)*SPV(IJKN)*ARE*FYN+
     *     SV(IJK)*SPV(IJK)*ARE*FYP
      DENN=DEN(IJKN)*FYN+DEN(IJK)*FYP
      VN=V(IJKN)*FYN+V(IJK)*FYP
     *   +COL*(PNSI-DN*(P(IJKN)-P(IJK)))
      IF(NGR.NE.NGIT) THEN
      VN1=V1(IJKN)*FYN+V1(IJK)*FYP
      VN=VN-VN1
      ENDIF
      F2(IJK)=DENN*VN*ARE
      IF(NGR.NE.NGIT) F2(IJK)=F2(IJK)+F2G(IJK)
      AN(IJK)=DN*DENN*ARE
      AS(IJKN)=AN(IJK)
      ENDIF
C
C.....TOP CELL FACE
      IF(K.NE.NKM) THEN
      ARE=DX*DY
      APER=SPW(IJKT)*FZT+SPW(IJK)*FZP
      DT=ARE*APER
      PTBI=SW(IJKT)*SPW(IJKT)*ARE*FZT +
     *     SW(IJK)*SPW(IJK)*ARE*FZP
      DENT=DEN(IJKT)*FZT+DEN(IJK)*FZP
      WT=W(IJKT)*FZT+W(IJK)*FZP
     * +COL*(PTBI-DT*(P(IJKT)-P(IJK)))
      IF(NGR.NE.NGIT) THEN
      WT1=W1(IJKT)*FZT+W1(IJK)*FZP
      WT=WT-WT1
      ENDIF
      F3(IJK)=DENT*WT*ARE
      IF(NGR.NE.NGIT) F3(IJK)=F3(IJK)+F3G(IJK)
      AT(IJK)=DT*DENT*ARE
      AB(IJKT)=AT(IJK)
      ENDIF
  200 CONTINUE

      SUM=0.
      DO 250 K=2,NKM
      MK=LK(K)
      DO 250 I=2,NIM
      MI=MK+LI(I)
      DO 250 IJK=MI+2,MI+NJM
C.....SOURCE TERMS
      SU(IJK)=F1(IJK-NJ)-F1(IJK)+F2(IJK-1)-F2(IJK)
     *      +F3(IJK-NIJ)-F3(IJK)
      SUM=SUM+SU(IJK)
  250 CONTINUE
C
C.....TEST   SUM=0      
      IF(LTEST) WRITE(6,600) SUM
C
C.....BOUNDARY CONDITIONS
      CALL BCPP
C
C.....FINAL ASSEMBLY
      DO 300 K=2,NKM
      MK=LK(K)
      DO 300 I=2,NIM
      MI=LI(I)+MK
      DO 300 IJK=MI+2,MI+NJM
      AP(IJK)=AE(IJK)+AW(IJK)+AN(IJK)
     *      +AS(IJK)+AT(IJK)+AB(IJK)
      PP(IJK)=0.
  300 CONTINUE
C
C.....SOLVE PP-EQUATION

      CALL SIPSOL(PP,IP)
C
C.....EXTRAPOLATE P' TO BOUNDARIES, CALCULATE P' DIFFERENCES
C.....EXTRAPOLATE PRESSURE CORRECTION
      CALL EXBPP
      CALL CALDPP
C
C.....CORRECT VELOCITIES, AMSS FLUXES AND PRESSURE
C
C.....CALCULATE REFERENT POINT
      IJKPR=LK(KPR)+LI(IPR)+JPR
      DO 400 K=2,NKM
      MK=LK(K)
      DZ=Z(K+KST)-Z(K+KST-1)
      DO 400 I=2,NIM
      MI=LI(I)+MK
      DX=X(I+IST)-X(I+IST-1)
      DO 400 IJK=MI+2,MI+NJM
      F1(IJK)=F1(IJK)+AE(IJK)*(PP(IJK)-PP(IJK+NJ))
      F2(IJK)=F2(IJK)+AN(IJK)*(PP(IJK)-PP(IJK+1))
      F3(IJK)=F3(IJK)+AT(IJK)*(PP(IJK)-PP(IJK+NIJ))
      J=IJK-MI
      DY=Y(J+JST)-Y(J+JST-1)
      U(IJK)=U(IJK)-DY*DZ*APR(IJK)*SU(IJK)
      V(IJK)=V(IJK)-DX*DZ*SPV(IJK)*SV(IJK)
      W(IJK)=W(IJK)-DX*DY*SPW(IJK)*SW(IJK)
      P(IJK)=P(IJK)+URF(IP)*(PP(IJK)-PP(IJKPR))
  400 CONTINUE
C
C.....TEST   SUM=0      
      IF(LTEST) THEN
      SUM=0.
      SUMA=0.
      DO 410 K=2,NKM
      MK=LK(K)
      DO 410 I=2,NIM
      MI=LI(I)+MK
      DO 410 IJK=MI+2,MI+NJM
C.....SOURCE TERMS
      SU(IJK)=F1(IJK-NJ)-F1(IJK)+F2(IJK-1)-F2(IJK)
     *      +F3(IJK-NIJ)-F3(IJK)
      SUM=SUM+SU(IJK)
      SUMA=SUMA+ABS(SU(IJK))
  410 CONTINUE
      WRITE(6,610) SUM,SUMA
      ENDIF
C
C.....EXTRAPOLATE NEW CORRECTED PRESSURE
      CALL EXBP
  600 FORMAT(20X,'SUM =',1PE10.3)
  610 FORMAT(20X,'SUM =',1PE10.3,/,20X,'SUMA=',1PE10.3)
      RETURN
      END
C***********************************************************************
      SUBROUTINE CALDP
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /GEO/ X(NXA),Y(NYA),Z(NZA),FX(NXA),FY(NYA),FZ(NZA)
      COMMON /VARF/ U(NXYZA),V(NXYZA),W(NXYZA),P(NXYZA),PP(NXYZA),
     *       T(NXYZA),F1(NXYZA),F2(NXYZA),F3(NXYZA),VIS(NXYZA),
     *       DEN(NXYZA)
      COMMON /VARG/ U1(NXYZM),V1(NXYZM),W1(NXYZM),
     *       T1(NXYZM),F1G(NXYZM),F2G(NXYZM),F3G(NXYZM),
     *       RESU(NXYZM),RESV(NXYZM),RESW(NXYZM),REST(NXYZM)
      COMMON /COEFF/ AE(NXYZA),AW(NXYZA),AN(NXYZA),AS(NXYZA),
     *         AB(NXYZA),AT(NXYZA),SU(NXYZA),APR(NXYZA),AP(NXYZA),
     *         SPV(NXYZA),SPW(NXYZA),SV(NXYZA),SW(NXYZA)
C
      DIMENSION PB(NXYZA),PW(NY)
C
C.....INITIALIZE BOTTOM CELL FACE PRESSURES
      DO 10 I=2,NIM
      MI=LI(I)
      DO 10 IJ=MI+2,MI+NJM
   10 PB(IJ)=P(LK(1)+IJ)
      DO 100 K=2,NKM
      MK=LK(K)
C.....INITIALIZE WEST CELL FACE PRESSURES
      DO 50 J=2,NJM
   50 PW(J)=P(MK+LI(1)+J)
      DO 100 I=2,NIM
      MI=MK+LI(I)
      PS=P(MI+1)
      DO 100 IJK=MI+2,MI+NJM
      J=IJK-MI
C.....EAST CELL FACE, PE-PW
      PE=P(IJK+NJ)*FX(I+IST)+P(IJK)*(1.-FX(I+IST))
      SU(IJK)=PE-PW(J)
      PW(J)=PE
C.....NORTH CELL FACE, PN-PS
      PN=P(IJK+1)*FY(J+JST)+P(IJK)*(1.-FY(J+JST))
      SV(IJK)=PN-PS
      PS=PN
C.....TOP CELL FACE, PT-PB
      PT=P(IJK+NIJ)*FZ(K+KST)+P(IJK)*(1.-FZ(K+KST))
      SW(IJK)=PT-PB(IJK-MK)
      PB(IJK-MK)=PT
  100 CONTINUE
      RETURN
      END
C***********************************************************************
      SUBROUTINE CALDPP
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /GEO/ X(NXA),Y(NYA),Z(NZA),FX(NXA),FY(NYA),FZ(NZA)
      COMMON /VARF/ U(NXYZA),V(NXYZA),W(NXYZA),P(NXYZA),PP(NXYZA),
     *       T(NXYZA),F1(NXYZA),F2(NXYZA),F3(NXYZA),VIS(NXYZA),
     *       DEN(NXYZA)
      COMMON /VARG/ U1(NXYZM),V1(NXYZM),W1(NXYZM),
     *       T1(NXYZM),F1G(NXYZM),F2G(NXYZM),F3G(NXYZM),
     *       RESU(NXYZM),RESV(NXYZM),RESW(NXYZM),REST(NXYZM)
      COMMON /COEFF/ AE(NXYZA),AW(NXYZA),AN(NXYZA),AS(NXYZA),
     *         AB(NXYZA),AT(NXYZA),SU(NXYZA),APR(NXYZA),AP(NXYZA),
     *         SPV(NXYZA),SPW(NXYZA),SV(NXYZA),SW(NXYZA)
C
      DIMENSION PPB(NXYZA),PPW(NY)
C
C.....INITIALIZE BOTTOM CELL FACE PRESSURES
      DO 10 I=2,NIM
      MI=LI(I)
      DO 10 IJ=MI+2,MI+NJM
   10 PPB(IJ)=PP(LK(1)+IJ)
      DO 100 K=2,NKM
      MK=LK(K)
C.....INITIALIZE WEST CELL FACE PRESSURES
      DO 50 J=2,NJM
   50 PPW(J)=PP(MK+LI(1)+J)
      DO 100 I=2,NIM
      MI=MK+LI(I)
      PPS=PP(MI+1)
      DO 100 IJK=MI+2,MI+NJM
      J=IJK-MI
C.....EAST CELL FACE, PPE - PPW
      PPE=PP(IJK+NJ)*FX(I+IST)+PP(IJK)*(1.-FX(I+IST))
      SU(IJK)=PPE-PPW(J)
      PPW(J)=PPE
C.....NORTH CELL FACE, PPN - PPS
      PPN=PP(IJK+1)*FY(J+JST)+PP(IJK)*(1.-FY(J+JST))
      SV(IJK)=PPN-PPS
      PPS=PPN
C.....TOP CELL FACE, PPT - PPB
      PPT=PP(IJK+NIJ)*FZ(K+KST)+PP(IJK)*(1.-FZ(K+KST))
      SW(IJK)=PPT-PPB(IJK-MK)
      PPB(IJK-MK)=PPT
  100 CONTINUE
      RETURN
      END
C***********************************************************************
      SUBROUTINE OUTBC
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /PARA/ GDS(NFI),URF(NFI),NSW(NFI),SOR(NFI),RESOR(NFI),
     *        SNOR(NFI),ALFA,SORMAX,SLARGE,FLOWIN,SMALL,GREAT
      COMMON /LOGIC/ LCAL(NFI),LTEST,LREAD,LWRITE,LSOL,LSOR,
     *         LOUTS,LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
      COMMON /GEO/ X(NXA),Y(NYA),Z(NZA),FX(NXA),FY(NYA),FZ(NZA)
      COMMON /VARF/ U(NXYZA),V(NXYZA),W(NXYZA),P(NXYZA),PP(NXYZA),
     *       T(NXYZA),F1(NXYZA),F2(NXYZA),F3(NXYZA),VIS(NXYZA),
     *       DEN(NXYZA)
      COMMON /VARG/ U1(NXYZM),V1(NXYZM),W1(NXYZM),
     *       T1(NXYZM),F1G(NXYZM),F2G(NXYZM),F3G(NXYZM),
     *       RESU(NXYZM),RESV(NXYZM),RESW(NXYZM),REST(NXYZM)
      COMMON /BC/ LBS(NXZA),LBN(NXZA),LBW(NYZA),LBE(NYZA),
     *            LBB(NXYA),LBT(NXYA)
      LOGICAL LCAL,LTEST,LREAD,LWRITE,LOUTS,LSOL,LSOR,
     *        LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
C
C.....EAST BOUNDARY OUTFLOW MASS FLUX ESTIMATE
      FLOWE = 0.
      IF(LOE) THEN
      DO 100 J=2,NJM
      MJ=LI(NIM)+J
      JJ=(J-1)*NK
      DY=Y(J+JST)-Y(J+JST-1)
      DO 100 K=2,NKM
      IF(LBE(JJ+K+JKST).EQ. 2) THEN
      IJK=LK(K)+MJ
      DZ=Z(K+KST)-Z(K+KST-1)
      F1(IJK)=DEN(IJK+NJ)*DZ*DY*U(IJK)
      FLOWE=FLOWE+F1(IJK)
      ENDIF
  100 CONTINUE
      ENDIF
C
C.....WEST BOUNDARY OUTFLOW MASS FLUX ESTIMATE
      FLOWW = 0.
      IF(LOW) THEN
      DO 150 J=2,NJM
      MJ=LI(1)+J
      JJ=(J-1)*NK
      DY=Y(J+JST)-Y(J+JST-1)
      DO 150 K=2,NKM
      IF(LBW(JJ+K+JKST) .EQ. 2) THEN
      IJK=LK(K)+MJ
      DZ=Z(K+KST)-Z(K+KST-1)
      F1(IJK)=DEN(IJK)*DZ*DY*U(IJK+NJ)
      FLOWW=FLOWW-F1(IJK)
      ENDIF
  150 CONTINUE
      ENDIF
C
C.....SOUTH BOUNDARY OUTFLOW MASS FLUX ESTIMATE
      FLOWS = 0.
      IF(LOS) THEN
      DO 200 I=2,NIM
      II=LI(I)+1
      KK=(I-1)*NK
      DX=X(I+IST)-X(I+IST-1)
      DO 200 K=2,NKM
      IF(LBS(KK+K+IKST) .EQ. 2) THEN
      IJK=LK(K)+II
      DZ=Z(K+KST)-Z(K+KST-1)
      F2(IJK)=DEN(IJK)*DZ*DX*V(IJK+1)
      FLOWS=FLOWS-F2(IJK)
      ENDIF
  200 CONTINUE
      ENDIF
C
C.....NORTH BOUNDARY OUTFLOW MASS FLUX ESTIMATE
      FLOWN = 0.
      IF(LON) THEN
      DO 250 I=2,NIM
      II=LI(I)+NJM
      KK=(I-1)*NK
      DX=X(I+IST)-X(I+IST-1)
      DO 250 K=2,NKM
      IF(LBN(KK+K+IKST).EQ. 2) THEN
      IJK=LK(K)+II
      DZ=Z(K+KST)-Z(K+KST-1)
      F2(IJK)=DEN(IJK+1)*DZ*DX*V(IJK)
      FLOWN=FLOWN+F2(IJK)
      ENDIF
  250 CONTINUE
      ENDIF
C
C.....BOTTOM BOUNDARY OUTFLOW MASS FLUX ESTIMATE
      FLOWB= 0.
      MK=LK(1)
      IF(LOB) THEN
      DO 300 I=2,NIM
      II=(I-1)*NJ
      DX=X(I+IST)-X(I+IST-1)
      DO 300 J=2,NJM
      IF(LBB(II+J+IJST).EQ.2) THEN
      IJK=MK+II+J
      DY=Y(J+JST)-Y(J+JST-1)
      F3(IJK)=DEN(IJK)*DX*DY*W(IJK+NIJ)
      FLOWB=FLOWB-F3(IJK)
      ENDIF
  300 CONTINUE
      ENDIF
C
C.....TOP BOUNDARY OUTFLOW MASS FLUX ESTIMATE
      FLOWT=0.
      MK= LK(NKM)
      IF(LOT) THEN
      DO 350 I=2,NIM
      II=(I-1)*NJ
      DX=X(I+IST)-X(I+IST-1)
      DO 350 J=2,NJM
      IF(LBT(II+J+IJST).EQ.2) THEN
      IJK=MK+II+J
      DY=Y(J+JST)-Y(J+JST-1)
      F3(IJK)=DEN(IJK+NIJ)*DX*DY*W(IJK)
      FLOWT=FLOWT+F3(IJK)
      ENDIF
  350 CONTINUE
      ENDIF
C
C.....TOTAL OUTFLOW MASS FLUX
      FLOWO=FLOWW+FLOWE+FLOWS+FLOWN+FLOWB+FLOWT
      FAC=FLOWIN/(FLOWO+SMALL)
C
C.....CORRECT OUTFLOW MASS FLUXES, SET VARIABLE VALUES
C
C.....EAST BOUNDARY
      IF(LOE) THEN
      DO 400 J=2,NJM
      MJ=LI(NIM)+J
      JJ=(J-1)*NK
      DO 400 K=2,NKM
      IF(LBE(JJ+K+JKST).EQ.2) THEN
      IJK=LK(K)+MJ
      F1(IJK)=F1(IJK)*FAC
      U(IJK+NJ)=U(IJK)*FAC
      V(IJK+NJ)=V(IJK)
      W(IJK+NJ)=W(IJK)
      ENDIF
  400 CONTINUE
      ENDIF
C.....WEST BOUNDARY
      IF(LOW) THEN
      DO 450 J=2,NJM
      MJ=LI(1)+J
      JJ=(J-1)*NK
      DO 450 K=2,NKM
      IF(LBW(JJ+K+JKST).EQ.2) THEN
      IJK=LK(K)+MJ
      F1(IJK)=F1(IJK)*FAC
      U(IJK)=U(IJK+NJ)*FAC
      V(IJK)=V(IJK+NJ)
      W(IJK)=W(IJK+NJ)
      ENDIF
  450 CONTINUE
      ENDIF
C.....SOUTH BOUNDARY
      IF(LOS) THEN
      DO 500 I=2,NIM
      II=LI(I)+1
      KK=(I-1)*NK
      DO 500 K=2,NKM
      IF(LBS(KK+K+IKST).EQ.2) THEN
      IJK=LK(K)+II
      F2(IJK)=F2(IJK)*FAC
      U(IJK)=U(IJK+1)
      V(IJK)=V(IJK+1)*FAC
      W(IJK)=W(IJK+1)
      ENDIF
  500 CONTINUE
      ENDIF
C.....NORTH BOUNDARY
      IF(LON) THEN
      DO 550 I=2,NIM
      II=LI(I)+NJM
      KK=(I-1)*NK
      DO 550 K=2,NKM
      IF(LBN(KK+K+IKST).EQ.2) THEN
      IJK=LK(K)+II
      F2(IJK)=F2(IJK)*FAC
      U(IJK+1)=U(IJK)
      V(IJK+1)=V(IJK)*FAC
      W(IJK+1)=W(IJK)
      ENDIF
  550 CONTINUE
      ENDIF
C.....BOTTOM BOUNDARY
      IF(LOB) THEN
      MK=LK(I)
      DO 600 I=2,NIM
      II=LI(I)
      DO 600 J=2,NJM
      IF(LBB(II+J+IJST).EQ.2) THEN
      IJK=MK+II+J
      F3(IJK)=F3(IJK)*FAC
      U(IJK)=U(IJK+NIJ)
      V(IJK)=V(IJK+NIJ)
      W(IJK)=W(IJK+NIJ)*FAC
      ENDIF
  600 CONTINUE
      ENDIF
C.....TOP BOUNDARY
      IF(LOT) THEN
      MK=LK(NKM)
      DO 650 I=2,NIM
      II=LI(I)
      DO 650 J=2,NJM
      IF(LBT(II+J+IJST).EQ.2) THEN
      IJK=MK+II+J
      F3(IJK)=F3(IJK)*FAC
      U(IJK+NIJ)=U(IJK)
      V(IJK+NIJ)=V(IJK)
      W(IJK+NIJ)=W(IJK)*FAC
      ENDIF
  650 CONTINUE
      ENDIF
C.....
      RETURN
      END
C***********************************************************************
      SUBROUTINE SYMUVW
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /PARA/ GDS(NFI),URF(NFI),NSW(NFI),SOR(NFI),RESOR(NFI),
     *        SNOR(NFI),ALFA,SORMAX,SLARGE,FLOWIN,SMALL,GREAT
      COMMON /LOGIC/ LCAL(NFI),LTEST,LREAD,LWRITE,LSOL,LSOR,
     *         LOUTS,LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
      COMMON /GEO/ X(NXA),Y(NYA),Z(NZA),FX(NXA),FY(NYA),FZ(NZA)
      COMMON /VARF/ U(NXYZA),V(NXYZA),W(NXYZA),P(NXYZA),PP(NXYZA),
     *       T(NXYZA),F1(NXYZA),F2(NXYZA),F3(NXYZA),VIS(NXYZA),
     *       DEN(NXYZA)
      COMMON /VARG/ U1(NXYZM),V1(NXYZM),W1(NXYZM),
     *       T1(NXYZM),F1G(NXYZM),F2G(NXYZM),F3G(NXYZM),
     *       RESU(NXYZM),RESV(NXYZM),RESW(NXYZM),REST(NXYZM)
      COMMON /COEFF/ AE(NXYZA),AW(NXYZA),AN(NXYZA),AS(NXYZA),
     *         AB(NXYZA),AT(NXYZA),SU(NXYZA),APR(NXYZA),AP(NXYZA),
     *         SPV(NXYZA),SPW(NXYZA),SV(NXYZA),SW(NXYZA)
      COMMON /BC/ LBS(NXZA),LBN(NXZA),LBW(NYZA),LBE(NYZA),
     *            LBB(NXYA),LBT(NXYA)
      LOGICAL LCAL,LTEST,LREAD,LWRITE,LOUTS,LSOL,LSOR,
     *        LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
C
C.....BOTTOM PLANE
      DO 150 I=2,NIM
      MI=LK(2)+LI(I)
      II=(I-1)*NJ
      DO 150 IJK=MI+2,MI+NJM
      J=IJK-MI
      IJKB=IJK-NIJ
      IJ=II+J
C.....SYMMETRY
      IF(LBB(IJ+IJST).EQ.3) THEN
      U1(IJKB)=U1(IJK)
      V1(IJKB)=V1(IJK)
      ENDIF
  150 CONTINUE
C
C.....TOP PLANE
      DO 200 I=2,NIM
      MI=LK(NKM)+LI(I)
      II=(I-1)*NJ
      DO 200 IJK=MI+2,MI+NJM
      J=IJK-MI
      IJKT=IJK+NIJ
      IJ=II+J
C.....SYMMETRY
      IF(LBT(IJ+IJST).EQ.3) THEN
      U1(IJKT)=U1(IJK)
      V1(IJKT)=V1(IJK)
      ENDIF
  200 CONTINUE
C
C.....WEST PLANE
      II=LI(2)
      DO 250 J=2,NJM
      JJ=(J-1)*NK
      DO 250 K=2,NKM
      IJK=LK(K)+II+J
      IJKW=IJK-NJ
      JK=JJ+K
C.....SYMMETRY
      IF(LBW(JK+JKST).EQ.3) THEN
      W1(IJKW)=W1(IJK)
      V1(IJKW)=V1(IJK)
      ENDIF
  250 CONTINUE
C
C.....EAST PLANE
      II=LI(NIM)
      DO 300 J=2,NJM
      JJ=(J-1)*NK
      DO 300 K=2,NKM
      IJK=LK(K)+II+J
      IJKE=IJK+NJ
      JK=JJ+K
C.....SYMMETRY
      IF(LBE(JK+JKST).EQ.3) THEN
      V1(IJKE)=V1(IJK)
      W1(IJKE)=W1(IJK)
      ENDIF
 300  CONTINUE
C
C.....SOUTH PLANE
      DO 350 I=2,NIM
      II=LI(I)+2
      KK=(I-1)*NK
      DO 350 K=2,NKM
      IJK=LK(K)+II
      IJKS=IJK-1
      IK=KK+K
C.....SYMMETRY
      IF(LBS(IK+IKST).EQ.3)THEN
      U1(IJKS)=U1(IJK)
      W1(IJKS)=W1(IJK)
      ENDIF
 350  CONTINUE
C
C.....NORTH PLANE
      DO 400 I=2,NIM
      II=LI(I)+NJM
      KK=(I-1)*NK
      DO 400 K=2,NKM
      IJK=LK(K)+II
      IJKN=IJK+1
      IK=KK+K
C.....SYMMETRY
      IF(LBN(IK+IKST).EQ.3) THEN
      U1(IJKN)=U1(IJK)
      W1(IJKN)=W1(IJK)
      ENDIF
 400  CONTINUE
      RETURN
      END
C***********************************************************************
      SUBROUTINE BCUVW
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /PARA/ GDS(NFI),URF(NFI),NSW(NFI),SOR(NFI),RESOR(NFI),
     *        SNOR(NFI),ALFA,SORMAX,SLARGE,FLOWIN,SMALL,GREAT
      COMMON /LOGIC/ LCAL(NFI),LTEST,LREAD,LWRITE,LSOL,LSOR,
     *         LOUTS,LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
      COMMON /GEO/ X(NXA),Y(NYA),Z(NZA),FX(NXA),FY(NYA),FZ(NZA)
      COMMON /VARF/ U(NXYZA),V(NXYZA),W(NXYZA),P(NXYZA),PP(NXYZA),
     *       T(NXYZA),F1(NXYZA),F2(NXYZA),F3(NXYZA),VIS(NXYZA),
     *       DEN(NXYZA)
      COMMON /VARG/ U1(NXYZM),V1(NXYZM),W1(NXYZM),
     *       T1(NXYZM),F1G(NXYZM),F2G(NXYZM),F3G(NXYZM),
     *       RESU(NXYZM),RESV(NXYZM),RESW(NXYZM),REST(NXYZM)
      COMMON /COEFF/ AE(NXYZA),AW(NXYZA),AN(NXYZA),AS(NXYZA),
     *         AB(NXYZA),AT(NXYZA),SU(NXYZA),APR(NXYZA),AP(NXYZA),
     *         SPV(NXYZA),SPW(NXYZA),SV(NXYZA),SW(NXYZA)
      COMMON /BC/ LBS(NXZA),LBN(NXZA),LBW(NYZA),LBE(NYZA),
     *            LBB(NXYA),LBT(NXYA)
      LOGICAL LCAL,LTEST,LREAD,LWRITE,LOUTS,LSOL,LSOR,
     *        LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
C
      CALL EXBP
C
C.....BOTTOM PLANE
      DZTR=2./(Z(2+KST)-Z(1+KST))
      DO 150 I=2,NIM
      MI=LK(2)+LI(I)
      II=(I-1)*NJ
      DX=X(I+IST)-X(I+IST-1)
      DO 150 IJK=MI+2,MI+NJM
      J=IJK-MI
      IJKB=IJK-NIJ
      IJ=II+J
      LBL=LBB(IJ+IJST)
      DY=Y(J+JST)-Y(J+JST-1)
      DT=VIS(IJKB)*DX*DY*DZTR
      SW(IJK)=SW(IJK)+P(IJKB)*DX*DY
C.....INLET
      IF(LBL.EQ.1) THEN
      AB(IJK)=DT+F3(IJKB)
C.....WALL
      ELSEIF (LBL.EQ.4.OR.LBL.EQ.41) THEN
      AP(IJK) = AP(IJK)+DT
      SPV(IJK)=SPV(IJK)+DT
      SU(IJK)=SU(IJK)+DT*U(IJKB)
      SV(IJK)=SV(IJK)+DT*V(IJKB)
C.....SYMMETRY
      ELSEIF (LBL.EQ.3) THEN
      SPW(IJK)=SPW(IJK)+DT
      U(IJKB)=U(IJK)
      V(IJKB)=V(IJK)
      ENDIF
  150 CONTINUE
C
C.....TOP PLANE
      DZTR=2./(Z(NKM+KST)-Z(NKM+KST-1))
      DO 200 I=2,NIM
      MI=LK(NKM)+LI(I)
      II=(I-1)*NJ
      DX=X(I+IST)-X(I+IST-1)
      DO 200 IJK=MI+2,MI+NJM
      J=IJK-MI
      IJKT=IJK+NIJ
      IJ=II+J
      LBL=LBT(IJ+IJST)
      DY=Y(J+JST)-Y(J+JST-1)
      DT=VIS(IJKT)*DX*DY*DZTR
      SW(IJK)=SW(IJK)-P(IJKT)*DX*DY
C.....INLET
      IF (LBL.EQ.1) THEN
      AT(IJK)=DT-F3(IJK)
C.....SYMMETRY
      ELSEIF (LBL.EQ.3) THEN
      SPW(IJK)=SPW(IJK)+DT
      U(IJKT)=U(IJK)
      V(IJKT)=V(IJK)
C.....WALL
      ELSEIF(LBL.EQ.4.OR.LBL.EQ.41) THEN
      AP(IJK)=AP(IJK)+DT
      SPV(IJK)=SPV(IJK)+DT
      SU(IJK)=SU(IJK)+DT*U(IJKT)
      SV(IJK)=SV(IJK)+DT*V(IJKT)
      ENDIF
  200 CONTINUE
C
C.....WEST PLANE
      DXER=2./(X(2+IST)-X(1+IST))
      II=LI(2)
      DO 250 J=2,NJM
      JJ=(J-1)*NK
      DY=Y(J+JST)-Y(J+JST-1)
      DO 250 K=2,NKM
      IJK=LK(K)+II+J
      IJKW=IJK-NJ
      JK=JJ+K
      LBL=LBW(JK+JKST)
      DZ=Z(K+KST)-Z(K+KST-1)
      DE=VIS(IJKW)*DZ*DY*DXER
      SU(IJK)=SU(IJK)+P(IJKW)*DZ*DY
C.....INLET
      IF (LBL.EQ.1) THEN
      AW(IJK)=DE+F1(IJKW)
C.....WALL
      ELSEIF(LBL.EQ.4.OR.LBL.EQ.41) THEN
      SPV(IJK)=SPV(IJK)+DE
      SPW(IJK)=SPW(IJK)+DE
      SV(IJK)=SV(IJK)+DE*V(IJKW)
      SW(IJK)=SW(IJK)+DE*W(IJKW)
C.....SYMMETRY
      ELSEIF (LBL.EQ.3) THEN
      AP(IJK)=AP(IJK)+DE
      W(IJKW)=W(IJK)
      V(IJKW)=V(IJK)
      ENDIF
  250 CONTINUE
C
C.....EAST PLANE
      DXER=2./(X(NIM+IST)-X(NIM+IST-1))
      II=LI(NIM)
      DO 300 J=2,NJM
      JJ=(J-1)*NK
      DY=Y(J+JST)-Y(J+JST-1)
      DO 300 K=2,NKM
      IJK=LK(K)+II+J
      IJKE=IJK+NJ
      JK=JJ+K
      LBL=LBE(JK+JKST)
      DZ=Z(K+KST)-Z(K+KST-1)
      DE=VIS(IJKE)*DZ*DY*DXER
      SU(IJK)=SU(IJK)-P(IJKE)*DZ*DY
C.....INLET
      IF (LBL.EQ.1) THEN
      AE(IJK)=DE-F1(IJK)
C.....WALL
      ELSEIF(LBL.EQ.4.OR.LBL.EQ.41) THEN
      SPV(IJK)=SPV(IJK)+DE
      SPW(IJK)=SPW(IJK)+DE
      SV(IJK)=SV(IJK)+DE*V(IJKE)
      SW(IJK)=SW(IJK)+DE*W(IJKE)
C.....SYMMETRY
      ELSEIF (LBL.EQ.3) THEN
      AP(IJK)=AP(IJK)+DE
      V(IJKE)=V(IJK)
      W(IJKE)=W(IJK)
      ENDIF
 300  CONTINUE
C
C.....SOUTH PLANE
      DYNR=2./(Y(2+JST)-Y(1+JST))
      DO 350 I=2,NIM
      II=LI(I)+2
      KK=(I-1)*NK
      DX=X(I+IST)-X(I+IST-1)
      DO 350 K=2,NKM
      IJK=LK(K)+II
      IJKS=IJK-1
      IK=KK+K
      LBL=LBS(IK+IKST)
      DZ=Z(K+KST)-Z(K+KST-1)
      DN=VIS(IJKS)*DX*DZ*DYNR
      SV(IJK)=SV(IJK)+P(IJKS)*DZ*DX
C.....INLET
      IF (LBL.EQ.1)THEN
      AS(IJK)=DN+F2(IJKS)
C.....WALL
      ELSEIF(LBL.EQ.4.OR.LBL.EQ.41) THEN
      AP(IJK)=AP(IJK)+DN
      SPW(IJK)=SPW(IJK)+DN
      SU(IJK)=SU(IJK)+DN*U(IJKS)
      SW(IJK)=SW(IJK)+DN*W(IJKS)
C.....SYMMETRY
      ELSEIF(LBL.EQ.3)THEN
      SPV(IJK)=SPV(IJK)+DN
      U(IJKS)=U(IJK)
      W(IJKS)=W(IJK)
      ENDIF
 350  CONTINUE
C
C.....NORTH PLANE
      DYNR=2./(Y(NJM+JST)-Y(NJM+JST-1))
      DO 400 I=2,NIM
      II=LI(I)+NJM
      KK=(I-1)*NK
      DX=X(I+IST)-X(I+IST-1)
      DO 400 K=2,NKM
      IJK=LK(K)+II
      IJKN=IJK+1
      IK=KK+K
      LBL=LBN(IK+IKST)
      DZ=Z(K+KST)-Z(K+KST-1)
      DN=VIS(IJKN)*DX*DZ*DYNR
      SV(IJK)=SV(IJK)-P(IJKN)*DZ*DX
C.....INLET
      IF (LBL.EQ.1) THEN
      AN(IJK)=DN-F2(IJK)
C.....WALL
      ELSEIF(LBL.EQ.4.OR.LBL.EQ.41)THEN
      AP(IJK)=AP(IJK)+DN
      SPW(IJK)=SPW(IJK)+DN
      SU(IJK)=SU(IJK)+DN*U(IJKN)
      SW(IJK)=SW(IJK)+DN*W(IJKN)
C.....SYMMETRY
      ELSEIF(LBL.EQ.3) THEN
      SPV(IJK)=SPV(IJK)+DN
      U(IJKN)=U(IJK)
      W(IJKN)=W(IJK)
      ENDIF
 400  CONTINUE
      RETURN
      END
C
C***********************************************************************
      SUBROUTINE BCPP
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /LOGIC/ LCAL(NFI),LTEST,LREAD,LWRITE,LSOL,LSOR,
     *         LOUTS,LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
      COMMON /GEO/ X(NXA),Y(NYA),Z(NZA),FX(NXA),FY(NYA),FZ(NZA)
      COMMON /COEFF/ AE(NXYZA),AW(NXYZA),AN(NXYZA),AS(NXYZA),
     *         AB(NXYZA),AT(NXYZA),SU(NXYZA),APR(NXYZA),AP(NXYZA),
     *         SPV(NXYZA),SPW(NXYZA),SV(NXYZA),SW(NXYZA)
      COMMON /BC/ LBS(NXZA),LBN(NXZA),LBW(NYZA),LBE(NYZA),
     *            LBB(NXYA),LBT(NXYA)
      LOGICAL LCAL,LTEST,LREAD,LWRITE,LOUTS,LSOL,LSOR,
     *        LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
C
C.....BOTTOM BOUNDARY
      MK=LK(2)
      DO 100 I=2,NIM
      MI=LI(I)+MK
      DO 100 IJK=MI+2,MI+NJM
C.....BOUNDARY TYPES 1,2,3 AND 4
      AB(IJK)=0.
  100 CONTINUE
C
C.....TOP BOUNDARY
      MK=LK(NKM)
      DO 150 I=2,NIM
      MI=LI(I)+MK
      DO 150 IJK=MI+2,MI+NJM
C.....BOUNDARY TYPES 1,2,3 AND 4
      AT(IJK)=0.
  150 CONTINUE
C
C-----WEST BOUNDARY
C
      MI=LI(2)
      DO 200 J=2,NJM
      MJ=MI+J
      DO 200 K=2,NKM
      IJK=LK(K)+MJ
C.....BOUNDARY TYPES 1,2,3 AND 4
      AW(IJK) = 0.
  200 CONTINUE
C
C.....EAST BOUNDARY
      MI=LI(NIM)
      DO 250 J=2,NJM
      MJ=MI+J
      DO 250 K=2,NKM
      IJK=LK(K)+MJ
C.....BOUNDARY TYPES 1,2,3 AND 4
      AE(IJK) = 0.
  250 CONTINUE
C
C.....SOUTH BOUNDARY
      DO 300 I=2,NIM
      MI=LI(I)+2
      DO 300 K=2,NKM
      IJK=LK(K)+MI
C-----BOUNDARY TYPES 1,2,3 AND 4
      AS(IJK)=0.
  300 CONTINUE
C
C.....NORTH BOUNDARY
      DO 350 I=2,NIM
      MI=LI(I)+NJM
      DO 350 K=2,NKM
      IJK=LK(K)+MI
C.....BOUNDARY TYPES 1,2,3 AND 4
      AN(IJK)=0.
  350 CONTINUE
C
      RETURN
      END
C
C***********************************************************************
      SUBROUTINE EXBP
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /LOGIC/ LCAL(NFI),LTEST,LREAD,LWRITE,LSOL,LSOR,
     *         LOUTS,LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
      COMMON /GEO/ X(NXA),Y(NYA),Z(NZA),FX(NXA),FY(NYA),FZ(NZA)
      COMMON /VARF/ U(NXYZA),V(NXYZA),W(NXYZA),P(NXYZA),PP(NXYZA),
     *       T(NXYZA),F1(NXYZA),F2(NXYZA),F3(NXYZA),VIS(NXYZA),
     *       DEN(NXYZA)
      COMMON /VARG/ U1(NXYZM),V1(NXYZM),W1(NXYZM),
     *       T1(NXYZM),F1G(NXYZM),F2G(NXYZM),F3G(NXYZM),
     *       RESU(NXYZM),RESV(NXYZM),RESW(NXYZM),REST(NXYZM)
      COMMON /BC/ LBS(NXZA),LBN(NXZA),LBW(NYZA),LBE(NYZA),
     *            LBB(NXYA),LBT(NXYA)
      LOGICAL LCAL,LTEST,LREAD,LWRITE,LOUTS,LSOL,LSOR,
     *        LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
C
C.....BOTTOM BOUNDARY
      MK=LK(2)
      DO 100 I=2,NIM
      MI=LI(I)+MK
      DO 100 IJK=MI+2,MI+NJM
      P(IJK-NIJ)=P(IJK)-(P(IJK+NIJ)-P(IJK))*FZ(2+KST)
  100 CONTINUE
C
C.....TOP BOUNDARY
      MK=LK(NKM)
      DO 150 I=2,NIM
      MI=LI(I)+MK
      DO 150 IJK=MI+2,MI+NJM
      P(IJK+NIJ)=P(IJK)+
     *       (P(IJK)-P(IJK-NIJ))*(1.-FZ(NKM-1+KST))
  150 CONTINUE
C
C.....WEST BOUNDARY
      MI=LI(2)
      DO 200 J=2,NJM
      MJ=MI+J
      DO 200 K=1,NK 
      IJK=LK(K)+MJ
      P(IJK-NJ)=P(IJK)-(P(IJK+NJ)-P(IJK))*FX(2+IST)
  200 CONTINUE
C
C.....EAST BOUNDARY
      MI=LI(NIM)
      DO 250 J=2,NJM
      MJ=MI+J
      DO 250 K=1,NK
      IJK=LK(K)+MJ
      P(IJK+NJ)=P(IJK)+
     *         (P(IJK)-P(IJK-NJ))*(1.-FX(NIM-1+IST))
  250 CONTINUE
C
C.....SOUTH BOUNDARY
      DO 300 I=1,NI
      MI=LI(I)+2
      DO 300 K=1,NK
      IJK=LK(K)+MI
      P(IJK-1)=P(IJK)-(P(IJK+1)-P(IJK))*FY(2+JST)
  300 CONTINUE
C
C.....NORTH BOUNDARY
      DO 350 I=1,NI
      MI=LI(I)+NJM
      DO 350 K=1,NK
      IJK=LK(K)+MI
      P(IJK+1)=P(IJK)+
     *        (P(IJK)-P(IJK-1))*(1.-FY(NJM-1+JST))
  350 CONTINUE
C
      RETURN
      END
C
C***********************************************************************
      SUBROUTINE EXBPP
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /LOGIC/ LCAL(NFI),LTEST,LREAD,LWRITE,LSOL,LSOR,
     *         LOUTS,LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
      COMMON /GEO/ X(NXA),Y(NYA),Z(NZA),FX(NXA),FY(NYA),FZ(NZA)
      COMMON /VARF/ U(NXYZA),V(NXYZA),W(NXYZA),P(NXYZA),PP(NXYZA),
     *       T(NXYZA),F1(NXYZA),F2(NXYZA),F3(NXYZA),VIS(NXYZA),
     *       DEN(NXYZA)
      COMMON /VARG/ U1(NXYZM),V1(NXYZM),W1(NXYZM),
     *       T1(NXYZM),F1G(NXYZM),F2G(NXYZM),F3G(NXYZM),
     *       RESU(NXYZM),RESV(NXYZM),RESW(NXYZM),REST(NXYZM)
      COMMON /BC/ LBS(NXZA),LBN(NXZA),LBW(NYZA),LBE(NYZA),
     *            LBB(NXYA),LBT(NXYA)
      LOGICAL LCAL,LTEST,LREAD,LWRITE,LOUTS,LSOL,LSOR,
     *        LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
C
C.....BOTTOM BOUNDARY
      MK=LK(2)
      DO 100 I=2,NIM
      MI=LI(I)+MK
      DO 100 IJK=MI+2,MI+NJM
      PP(IJK-NIJ)=PP(IJK)-(PP(IJK+NIJ)-PP(IJK))*FZ(2+KST)
  100 CONTINUE
C
C.....TOP BOUNDARY
      MK=LK(NKM)
      DO 150 I=2,NIM
      MI=LI(I)+MK
      DO 150 IJK=MI+2,MI+NJM
      PP(IJK+NIJ)=PP(IJK)+
     *         (PP(IJK)-PP(IJK-NIJ))*(1.-FZ(NKM-1+KST))
  150 CONTINUE
C
C.....WEST BOUNDARY
      MI=LI(2)
      DO 200 J=2,NJM
      MJ=MI+J
      DO 200 K=2,NKM
      IJK=LK(K)+MJ
      PP(IJK-NJ)=PP(IJK)-(PP(IJK+NJ)-PP(IJK))*FX(2+IST)
  200 CONTINUE
C
C.....EAST BOUNDARY
      MI=LI(NIM)
      DO 250 J=2,NJM
      MJ=MI+J
      DO 250 K=2,NKM
      IJK=LK(K)+MJ
      PP(IJK+NJ)=PP(IJK)+
     *          (PP(IJK)-PP(IJK-NJ))*(1.-FX(NIM-1+IST))
  250 CONTINUE
C
C.....SOUTH BOUNDARY
      DO 300 I=2,NIM
      MI=LI(I)+2
      DO 300 K=2,NKM
      IJK=LK(K)+MI
      PP(IJK-1)=PP(IJK)-(PP(IJK+1)-PP(IJK))*FY(2+JST)
  300 CONTINUE
C
C.....NORTH BOUNDARY
      DO 350 I=2,NIM
      MI=LI(I)+NJM
      DO 350 K=2,NKM
      IJK=LK(K)+MI
      PP(IJK+1)=PP(IJK)+
     *         (PP(IJK)-PP(IJK-1))*(1.-FY(NJM-1+JST))
  350 CONTINUE
C
      RETURN
      END
C***********************************************************************
      SUBROUTINE VINT(NGR,FIG,FI)
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA),
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /GEO/ X(NXA),Y(NYA),Z(NZA),FX(NXA),FY(NYA),FZ(NZA)
      DIMENSION FI(NXYZA),FIG(NXYZA)
C
C.....COARSE GRID
      NIG=NIGIT(NGR)
      NJG=NJGIT(NGR)
      NKG=NKGIT(NGR)
      ISTG=IGIT(NGR)
      JSTG=JGIT(NGR)
      KSTG=KGIT(NGR)
      IJKSTG=IJKGIT(NGR)
      NIMG=NIG-1
      NJMG=NJG-1
      NKMG=NKG-1
      NIJG=NIG*NJG
C.....FINE GRID
      NIF=NIGIT(NGR+1)
      NJF=NJGIT(NGR+1)
      NKF=NKGIT(NGR+1)
      ISTF=IGIT(NGR+1)
      JSTF=JGIT(NGR+1)
      KSTF=KGIT(NGR+1)
      IJKSTF=IJKGIT(NGR+1)
      NIJF=NIF*NJF
C.....
      DO 10 I=1,NIG
   10 LIG(I)=(I-1)*NJG+IJKSTG
      DO 20 K=1,NKG
   20 LKG(K)=(K-1)*NIJG
C....
      DO 30 I=1,NIF
   30 LI(I)=(I-1)*NJF+IJKSTF
      DO 40 K=1,NKF
   40 LK(K)=(K-1)*NIJF
C.....MAIN LOOP
C.....START INTERPOLATION FOR FINE GRID NODES
      DO 100 KG=1,NKMG
      KF=2*KG-1
      MKG=LKG(KG)
      MKF=LK(KF)
      DO 100 IG=1,NIMG
      IF=2*IG-1
      MIG=MKG+LIG(IG)
      MIF=MKF+LI(IF)
      DO 100 JG=1,NJMG
      JF=2*JG-1
      IJKG=MIG+JG
      IJKF=MIF+JF
C.....PARAMETERS FOR POINTS BETWEEN EIGHT COARSE GRID NODES
C.....BOTTOM 
C.....COARSE
      IGSWB=IJKG
      IGSEB=IGSWB+NJG
      IGNWB=IGSWB+1
      IGNEB=IGSEB+1
C.....FINE
      IFSWB=IJKF
      IFSEB=IFSWB+NJF
      IFNWB=IFSWB+1
      IFNEB=IFSEB+1
C.....TOP
C.....COARSE 
      IGSWT=IGSWB+NIJG
      IGSET=IGSWT+NJG
      IGNWT=IGSWT+1
      IGNET=IGSET+1 
C.....FINE 
      IFSWT=IFSWB+NIJF
      IFSET=IFSWT+NJF
      IFNWT=IFSWT+1
      IFNET=IFSET+1
C********************************************
C.....INTERPOLATION FACTORS
C.....BOTTOM
C     FXSWB=FX(IGSWB)*FX(IFSWB-NJF)
C     FXNWB=FX(IGNWB)*FX(IFNWB-NJF)
C     FXSEB=(1.-FX(IGSWB))*(1.-FX(IFSEB))
C     FXNEB=(1.-FX(IGNWB))*(1.-FX(IFNEB))
C.....TOP    
C     FXSWT=FX(IGSWT)*FX(IFSWT-NJF)
C     FXNWT=FX(IGNWT)*FX(IFNWT-NJF)
C     FXSET=(1.-FX(IGSWT))*(1.-FX(IFSET))
C     FXNET=(1.-FX(IGNWT))*(1.-FX(IFNET))
C.....BOTTOM
C     FYSWB=FY(IGSWB)*FY(IFSWB-1)
C     FYSEB=FY(IGSEB)*FY(IFSEB-1)
C     FYNWB=(1.-FY(IGSWB))*(1.-FY(IFNWB))
C     FYNEB=(1.-FY(IGSEB))*(1.-FY(IFNEB))
C.....TOP    
C     FYSWT=FY(IGSWT)*FY(IFSWT-1)
C     FYSET=FY(IGSET)*FY(IFSET-1)
C     FYNWT=(1.-FY(IGSWT))*(1.-FY(IFNWT))
C     FYNET=(1.-FY(IGSET))*(1.-FY(IFNET))
C.....BOTTOM
C     FZSWB=FZ(IGSWB)*FZ(IFSWB-NIJF)
C     FZSEB=FZ(IGSEB)*FZ(IFSEB-NIJF)
C     FZNWB=FZ(IGNWB)*FZ(IFNWB-NIJF)
C     FZNEB=FZ(IGNEB)*FZ(IFNEB-NIJF)
C.....TOP
C     FZSWT=(1.-FZ(IGSWB))*(1.-FZ(IFSWT))
C     FZSET=(1.-FZ(IGSEB))*(1.-FZ(IFSET))
C     FZNWT=(1.-FZ(IGNWB))*(1.-FZ(IFNWT))
C     FZNET=(1.-FZ(IGNEB))*(1.-FZ(IFNET))
C********************************************
C.....INTERPOLATION FACTORS
C.....BOTTOM
      FXSWB=FX(IG+ISTG)*FX(IF+ISTF-1)
      FXNWB=FX(IG+ISTG)*FX(IF+ISTF-1)
      FXSEB=(1.-FX(IG+ISTG))*(1.-FX(IF+ISTF+1))
      FXNEB=(1.-FX(IG+ISTG))*(1.-FX(IF+ISTF+1))
C.....TOP    
      FXSWT=FX(IG+ISTG)*FX(IF+ISTF-1) 
      FXNWT=FX(IG+ISTG)*FX(IF+ISTF-1) 
      FXSET=(1.-FX(IG+ISTG))*(1.-FX(IF+ISTF+1)) 
      FXNET=(1.-FX(IG+ISTG))*(1.-FX(IF+ISTF+1)) 
C.....BOTTOM
      FYSWB=FY(JG+JSTG)*FY(JF+JSTF-1)
      FYSEB=FY(JG+JSTG)*FY(JF+JSTF-1)
      FYNWB=(1.-FY(JG+JSTG))*(1.-FY(JF+JSTF+1))
      FYNEB=(1.-FY(JG+JSTG))*(1.-FY(JF+JSTF+1))
C.....TOP    
      FYSWT=FY(JG+JSTG)*FY(JF+JSTF-1)
      FYSET=FY(JG+JSTG)*FY(JF+JSTF-1)
      FYNWT=(1.-FY(JG+JSTG))*(1.-FY(JF+JSTF+1))
      FYNET=(1.-FY(JG+JSTG))*(1.-FY(JF+JSTF+1))
C.....BOTTOM
      FZSWB=FZ(KG+KSTG)*FZ(KF+KSTF-1)
      FZSEB=FZ(KG+KSTG)*FZ(KF+KSTF-1)
      FZNWB=FZ(KG+KSTG)*FZ(KF+KSTF-1)
      FZNEB=FZ(KG+KSTG)*FZ(KF+KSTF-1)
C.....TOP
      FZSWT=(1.-FZ(KG+KSTG))*(1.-FZ(KF+KSTF+1))
      FZSET=(1.-FZ(KG+KSTG))*(1.-FZ(KF+KSTF+1))
      FZNWT=(1.-FZ(KG+KSTG))*(1.-FZ(KF+KSTF+1))
      FZNET=(1.-FZ(KG+KSTG))*(1.-FZ(KF+KSTF+1))
C********************************************
C.....BOTTOM
      FISWBY=FIG(IGSWB)*(1.-FYSWB)+FIG(IGNWB)*FYSWB
      FISEBY=FIG(IGSEB)*(1.-FYSEB)+FIG(IGNEB)*FYSEB
      FINWBY=FIG(IGNWB)*(1.-FYNWB)+FIG(IGSWB)*FYNWB
      FINEBY=FIG(IGNEB)*(1.-FYNEB)+FIG(IGSEB)*FYNEB
C.....TOP
      FISWTY=FIG(IGSWT)*(1.-FYSWT)+FIG(IGNWT)*FYSWT
      FISETY=FIG(IGSET)*(1.-FYSET)+FIG(IGNET)*FYSET
      FINWTY=FIG(IGNWT)*(1.-FYNWT)+FIG(IGSWT)*FYNWT
      FINETY=FIG(IGNET)*(1.-FYNET)+FIG(IGSET)*FYNET
C.....BOTTOM
      FISWBX=FISWBY*(1.-FXSWB)+FISEBY*FXSWB
      FISEBX=FISEBY*(1.-FXSEB)+FISWBY*FXSEB
      FINWBX=FINWBY*(1.-FXNWB)+FINEBY*FXNWB
      FINEBX=FINEBY*(1.-FXNEB)+FINWBY*FXNEB
C.....TOP
      FISWTX=FISWTY*(1.-FXSWT)+FISETY*FXSWT
      FISETX=FISETY*(1.-FXSET)+FISWTY*FXSET  
      FINWTX=FINWTY*(1.-FXNWT)+FINETY*FXNWT
      FINETX=FINETY*(1.-FXNET)+FINWTY*FXNET
C.....ENDE
C.....BOTTOM
      FI(IFSWB)=FISWBX*(1-FZSWB)+FISWTX*FZSWB
      FI(IFSEB)=FISEBX*(1-FZSEB)+FISETX*FZSEB
      FI(IFNWB)=FINWBX*(1-FZNWB)+FINWTX*FZNWB
      FI(IFNEB)=FINEBX*(1-FZNEB)+FINETX*FZNEB
C.....TOP 
      FI(IFSWT)=FISWTX*(1-FZSWT)+FISWBX*FZSWT
      FI(IFSET)=FISETX*(1-FZSET)+FISEBX*FZSET
      FI(IFNWT)=FINWTX*(1-FZNWT)+FINWBX*FZNWT
      FI(IFNET)=FINETX*(1-FZNET)+FINEBX*FZNET
  100 CONTINUE
      RETURN
      END
C
C***********************************************************************
      SUBROUTINE SIPSOL(FI,IFI)
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /PARA/ GDS(NFI),URF(NFI),NSW(NFI),SOR(NFI),RESOR(NFI),
     *        SNOR(NFI),ALFA,SORMAX,SLARGE,FLOWIN,SMALL,GREAT
      COMMON /LOGIC/ LCAL(NFI),LTEST,LREAD,LWRITE,LSOL,LSOR,
     *         LOUTS,LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
      COMMON /COEFF/ AE(NXYZA),AW(NXYZA),AN(NXYZA),AS(NXYZA),
     *         AB(NXYZA),AT(NXYZA),SU(NXYZA),APR(NXYZA),AP(NXYZA),
     *         SPV(NXYZA),SPW(NXYZA),SV(NXYZA),SW(NXYZA)
      DIMENSION BB(NXYZA),BW(NXYZA),BS(NXYZA),BP(NXYZA),BN(NXYZA),
     *          BE(NXYZA),BT(NXYZA),RES(NXYZA),FI(NXYZA)
      LOGICAL LCAL,LTEST,LREAD,LWRITE,LOUTS,LSOL,LSOR,
     *        LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
C
C.....CALCULATE COEFFICIENTS OF  L  AND  U  MATRICES
      DO 20 K=2, NKM
      LKK=LK(K)
      DO 20 I=2, NIM
      LKI=LI(I)+LKK
      DO 20 IJK=LKI+2,LKI+NJM
      IMJK=IJK-NJ
      IJKM=IJK-NIJ
      IJMK=IJK-1
      BB(IJK)=-AB(IJK)/(1.+ALFA*(BN(IJKM)+BE(IJKM)))
      BW(IJK)=-AW(IJK)/(1.+ALFA*(BN(IMJK)+BT(IMJK)))
      BS(IJK)=-AS(IJK)/(1.+ALFA*(BE(IJMK)+BT(IJMK)))
      P1=ALFA*(BB(IJK)*BN(IJKM)+BW(IJK)*BN(IMJK))
      P2=ALFA*(BB(IJK)*BE(IJKM)+BS(IJK)*BE(IJMK))
      P3=ALFA*(BW(IJK)*BT(IMJK)+BS(IJK)*BT(IJMK))
      BP(IJK)=1./(AP(IJK)+P1+P2+P3-BB(IJK)*BT(IJKM)-BW(IJK)*BE(IMJK)
     *      -BS(IJK)*BN(IJMK)+SMALL)
      BN(IJK)=(-AN(IJK)-P1)*BP(IJK)
      BE(IJK)=(-AE(IJK)-P2)*BP(IJK)
      BT(IJK)=(-AT(IJK)-P3)*BP(IJK)
   20 CONTINUE
C
C.....ITERATION LOOP
      NS=NSW(IFI)
      DO 100 N=1,NS
C.....CALCULATE RESIDUALS AND AUXILLIARY VECTOR
      RES1=0.0
      DO 30 K=2,NKM
      LKK=LK(K)
      DO 30 I=2,NIM
      LKI=LKK+LI(I)
      DO 30 IJK=LKI+2,LKI+NJM
      RES(IJK)=AE(IJK)*FI(IJK+NJ)+AW(IJK)*FI(IJK-NJ)+AN(IJK)*
     *       FI(IJK+1)+AS(IJK)*FI(IJK-1)+AT(IJK)*FI(IJK+NIJ)+
     *       AB(IJK)*FI(IJK-NIJ)+SU(IJK)-AP(IJK)*FI(IJK)
      RES1=RES1+ABS(RES(IJK))
      RES(IJK)=(RES(IJK)-BB(IJK)*RES(IJK-NIJ)-BW(IJK)*RES(IJK-NJ)-
     *      BS(IJK)*RES(IJK-1))*BP(IJK)
   30 CONTINUE
      IF(N.EQ.1) RESOR(IFI)=RES1
      RSM=RES1/(RESOR(IFI)+SMALL)
C.....CALCULATE INCREMENT AND UPDATE VARIABLES
      DO 40 K=NKM,2,-1
      LKK=LK(K)
      DO 40 I=NIM,2,-1
      LKI=LKK+LI(I)
      DO 40 IJK=LKI+NJM,LKI+2,-1
      RES(IJK)=RES(IJK)-BN(IJK)*RES(IJK+1)-BE(IJK)*RES(IJK+NJ)-
     *       BT(IJK)*RES(IJK+NIJ)
      FI(IJK)=FI(IJK)+RES(IJK)
   40 CONTINUE
      IF(LTEST) WRITE(6,600) IFI,N,RES1
  600 FORMAT(20X,'FI=',I2,'  SWEEP=',I3,'  RES=',1PE10.3)
      IF(RSM.LT.SOR(IFI)) RETURN
  100 CONTINUE
      RETURN
      END
C
C***********************************************************************
      SUBROUTINE PRINTK(FI,HEDFI,NKE)
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
C.....PRINT FIELD VALUES IN SURFACES OF CONSTANT  K
      DIMENSION FI(NXYZA)
      CHARACTER*10 HEDFI
      WRITE(6,20) HEDFI
      DO 100 K=1,NKE
      WRITE(6,21) K
      LKK=LK(K)
      IS=-11
   10 IS=IS+12
      IE=IS+11
      IE=MIN0(IE,NI)
      WRITE(6,22) (I,I=IS,IE)
      WRITE(6,*) '  J'
      DO 11 J=NJ,1,-1
   11 WRITE(6,23) J,(FI(LKK+LI(I)+J),I=IS,IE)
      IF(IE.LT.NI) GO TO 10
  100 CONTINUE
      RETURN
   20 FORMAT(//,40X,A10,/,40X,10('*'),/)
   21 FORMAT(/,2X,26('*-'),7X,' K =',I3,' ',7X,26('-*'))
   22 FORMAT(3X,'I = ',I3,11I10)
   23 FORMAT(1X,I3,1P12E10.2)
      END
C
C***********************************************************************
      SUBROUTINE PRINTI(FI,HEDFI,NIE)
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
C.....PRINT FIELD VALUES IN SURFACES OF CONSTANT  I
      DIMENSION FI(NXYZA)
      CHARACTER*10 HEDFI
      WRITE(6,20) HEDFI
      DO 100 I=1,NIE
      II=LI(I)
      WRITE(6,21) I
      JS=-11
   10 JS=JS+12
      JE=JS+11
      JE=MIN0(JE,NJ)
      WRITE(6,22) (J,J=JS,JE)
      WRITE(6,*) '  K'
      DO 11 K=NK,1,-1
   11 WRITE(6,23) K,(FI(LK(K)+II+J),J=JS,JE)
      IF(JE.LT.NJ) GO TO 10
  100 CONTINUE
      RETURN
   20 FORMAT(//,40X,A10,/,40X,10('*'),/)
   21 FORMAT(/,2X,26('*-'),7X,' I =',I3,' ',7X,26('-*'))
   22 FORMAT(3X,'J = ',I3,11I10)
   23 FORMAT(1X,I3,1P12E10.2)
      END
C
C***********************************************************************
      SUBROUTINE PRINTJ(FI,HEDFI,NJE)
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
C.....PRINT FIELD VALUES IN SURFACES OF CONSTANT  J
      DIMENSION FI(NXYZA)
      CHARACTER*10 HEDFI
      WRITE(6,20) HEDFI
      DO 100 J=1,NJE
      WRITE(6,21) J
      IS=-11
   10 IS=IS+12
      IE=IS+11
      IE=MIN0(IE,NI)
      WRITE(6,22) (I,I=IS,IE)
      WRITE(6,*) '  K'
      DO 11 K=NK,1,-1
   11 WRITE(6,23) K,(FI(LK(K)+LI(I)+J),I=IS,IE)
      IF(IE.LT.NI) GO TO 10
  100 CONTINUE
      RETURN
   20 FORMAT(//,40X,A10,/,40X,10('*'),/)
   21 FORMAT(/,2X,26('*-'),7X,' J =',I3,' ',7X,26('-*'))
   22 FORMAT(3X,'I = ',I3,11I10)
   23 FORMAT(1X,I3,1P12E10.2)
      END
C
C***********************************************************************
      SUBROUTINE USER
C***********************************************************************
      PARAMETER (NGIT=4,NICV=8,NJCV=8,NKCV=8,NFI=5,
     *          NX=NICV*2**(NGIT-1)+2,NY=NJCV*2**(NGIT-1)+2,
     *          NZ=NKCV*2**(NGIT-1)+2,
     *          NXY=NX*NY,NXZ=NX*NZ,NYZ=NY*NZ,NXYZ=NXY*NZ,
     *          NXA=NICV*(2**NGIT-1)+2*NGIT,NYA=NJCV*(2**NGIT-1)+2*NGIT,
     *          NZA=NKCV*(2**NGIT-1)+2*NGIT,
     *          NXYA=NICV*NJCV*(4**NGIT-1)/3+2*(NXA+NYA-2*NGIT),
     *          NXZA=NICV*NKCV*(4**NGIT-1)/3+2*(NXA+NZA-2*NGIT),
     *          NYZA=NJCV*NKCV*(4**NGIT-1)/3+2*(NYA+NZA-2*NGIT),
     *          NXYZA=NICV*NJCV*NKCV*(8**NGIT-1)/7+
     *          2*(4**NGIT-1)/3*(NICV*NJCV+NICV*NKCV+NJCV*NKCV)+
     *          4*(NXA+NYA+NZA-4*NGIT),NXYZM=NXYZA-NXYZ+1)
      COMMON /INDEX/ NI,NJ,NK,NIM,NJM,NKM,NIJ,NIK,NJK,NIJK,KGRID,
     *         LI(NXA),LK(NZA),IPR,JPR,KPR,IMON,JMON,KMON,MAXIT,
     *         NIGIT(NGIT),NJGIT(NGIT),NKGIT(NGIT),IGIT(NGIT),
     *         JGIT(NGIT),KGIT(NGIT),IJKGIT(NGIT),
     *         ISBIJ(NGIT),ISBIK(NGIT),ISBJK(NGIT),LIG(NXA),LKG(NZA), 
     *         LSG(NGIT),LSR(NGIT),LSI(NGIT),MIT(NGIT),ITERF,
     *         IU,IV,IW,IP,IEN,IJST,IKST,JKST,IJKST,IST,JST,KST
      COMMON /PARA/ GDS(NFI),URF(NFI),NSW(NFI),SOR(NFI),RESOR(NFI),
     *        SNOR(NFI),ALFA,SORMAX,SLARGE,FLOWIN,SMALL,GREAT
      COMMON /LOGIC/ LCAL(NFI),LTEST,LREAD,LWRITE,LSOL,LSOR,
     *         LOUTS,LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
      COMMON /GEO/ X(NXA),Y(NYA),Z(NZA),FX(NXA),FY(NYA),FZ(NZA)
      COMMON /VARF/ U(NXYZA),V(NXYZA),W(NXYZA),P(NXYZA),PP(NXYZA),
     *       T(NXYZA),F1(NXYZA),F2(NXYZA),F3(NXYZA),VIS(NXYZA),
     *       DEN(NXYZA)
      COMMON /VARG/ U1(NXYZM),V1(NXYZM),W1(NXYZM),
     *       T1(NXYZM),F1G(NXYZM),F2G(NXYZM),F3G(NXYZM),
     *       RESU(NXYZM),RESV(NXYZM),RESW(NXYZM),REST(NXYZM)
      COMMON /COEFF/ AE(NXYZA),AW(NXYZA),AN(NXYZA),AS(NXYZA),
     *         AB(NXYZA),AT(NXYZA),SU(NXYZA),APR(NXYZA),AP(NXYZA),
     *         SPV(NXYZA),SPW(NXYZA),SV(NXYZA),SW(NXYZA)
      COMMON /BC/ LBS(NXZA),LBN(NXZA),LBW(NYZA),LBE(NYZA),
     *            LBB(NXYA),LBT(NXYA)
      LOGICAL LCAL,LTEST,LREAD,LWRITE,LOUTS,LSOL,LSOR,
     *        LOUTE,LPRI,LPRJ,LPRK,LOE,LOW,LOS,LON,LOB,LOT
      CHARACTER TITLE*40
      dimension uprof(nza),wprof(nxa),zprof(nza),xprof(nxa)
C
C***********************************************************************
      ENTRY INIT
C***********************************************************************
C
C.....READ INPUT DATA
C
      READ(5,5) TITLE
    5 FORMAT(A40)
      READ(5,*) LREAD,LWRITE,LTEST,LOUTS,LOUTE,LPRI,LPRJ,LPRK
      READ(5,*) (LCAL(I),I=1,NFI)
      READ(5,*) IMON,JMON,KMON,IPR,JPR,KPR
      READ(5,*) SORMAX,SLARGE,ALFA
      READ(5,*) (GDS(I),I=1,NFI)
      READ(5,*) (URF(I),I=1,NFI)
      READ(5,*) (SOR(I),I=1,NFI)
      READ(5,*) (NSW(I),I=1,NFI)
      READ(5,*) (LSG(I),I=1,NGIT)
      READ(5,*) (LSR(I),I=1,NGIT)
      READ(5,*) (LSI(I),I=1,NGIT)
      READ(5,*) (MIT(I),I=1,NGIT)
      READ(5,*) DENSIT,VISCOS,UMEAN,REFL
      READ(5,*) UIN,VIN,WIN,TIN
      READ(5,*) UWALL,VWALL,WWALL,TWALL
C
      IU=1
      IV=2
      IW=3
      IP=4
      IEN=5
      SMALL=1.E-30
      GREAT=1.E+30
      REY=DENSIT*UMEAN*REFL/VISCOS
C
C
C.....READ GRID DATA
C
      READ(4,*) LOB,LOT,LOS,LON,LOW,LOE,LBS,LBN,LBW,LBE,
     *           LBB,LBT,ISBIJ,ISBIK,ISBJK,
     *           NIGIT,NJGIT,NKGIT,IGIT,JGIT,KGIT,IJKGIT 
      READ(4,*)  X,Y,Z
C
C.....SET INDICES, CALCULATE INTERPOLATION FACTORS
C
      DO 45 NGR=1,NGIT
      CALL SETIND(NGR)
      DO 20 I=2+IST,NIM+IST
   20 FX(I)=(X(I)-X(I-1))/(X(I+1)-X(I-1))
      DO 30 J=2+JST,NJM+JST
   30 FY(J)=(Y(J)-Y(J-1))/(Y(J+1)-Y(J-1))
      DO 40 K=2+KST,NKM+KST
   40 FZ(K)=(Z(K)-Z(K-1))/(Z(K+1)-Z(K-1))
   45 CONTINUE
C
C.....INITIALIZE INTERIOR VARIABLE VALUES, DENSITY AND VISCOSITY
C
      DO 50 IJK=1,NXYZA
      VIS(IJK)=VISCOS
   50 DEN(IJK)=DENSIT
      RETURN
C
C########################################################
      ENTRY BCIN
C#########################################################
C
C.....SET INLET AND WALL VALUES, CALC. INLET MASS FLUXES
C
      CALL SETIND(KGRID)
      FLOWIN=0.
      XMOMIN=0.
C.....BOTTOM
      DO 55 I=2,NIM
      II=(I-1)*NJ
      DO 55 J=2,NJM
      IJ=II+J
      IJK=LK(1)+LI(I)+J
      IF(LBB(IJ+IJST).EQ.1) THEN
      U(IJK)=UIN
      V(IJK)=VIN
      W(IJK)=WIN
      T(IJK)=TIN
      F3(IJK)=DENSIT*W(IJK)*(X(I+IST)-X(I+IST-1))*(Y(J+JST)-Y(J+JST-1))
      XMOMIN=XMOMIN+F3(IJK)*W(IJK)
      FLOWIN=FLOWIN+F3(IJK)
      ELSEIF(LBB(IJ+IJST).EQ.41) THEN
      U(IJK)=UWALL
      V(IJK)=VWALL
      W(IJK)=WWALL
      T(IJK)=TWALL
      ENDIF
   55 CONTINUE
C.....TOP
      DO 65 I=2,NIM
      II=(I-1)*NJ
      DO 65 J=2,NJM
      IJ=II+J
      IJK=LK(NK)+LI(I)+J
      IF(LBT(IJ+IJST).EQ.1) THEN
      U(IJK)=UIN
      V(IJK)=VIN
      W(IJK)=WIN
      T(IJK)=TIN
      F3(IJK-NIJ)=DENSIT*W(IJK)*(X(I+IST)-X(I+IST-1))
     *                         *(Y(J+JST)-Y(J+JST-1))
      XMOMIN=XMOMIN+F3(IJK-NIJ)*W(IJK)
      FLOWIN=FLOWIN-F3(IJK-NIJ)
      ELSEIF(LBT(IJ+IJST).EQ.41) THEN
      U(IJK)=UWALL 
      V(IJK)=VWALL 
      W(IJK)=WWALL 
      T(IJK)=TWALL
      ENDIF
   65 CONTINUE
C.....SOUTH
      DO 75 I=2,NIM
      II=(I-1)*NK
      DO 75 K=2,NKM
      IK=II+K
      IJK=LK(K)+LI(I)+1
      IF(LBS(IK+IKST).EQ.1) THEN
      U(IJK)=UIN
      V(IJK)=VIN
      W(IJK)=WIN
      T(IJK)=TIN
      F2(IJK)=DENSIT*V(IJK)*(X(I+IST)-X(I+IST-1))*(Z(K+KST)-Z(K+KST-1))
      XMOMIN=XMOMIN+F2(IJK)*V(IJK)
      FLOWIN=FLOWIN+F2(IJK)
      ELSEIF(LBS(IK+IKST).EQ.41) THEN
      U(IJK)=UWALL 
      V(IJK)=VWALL  
      W(IJK)=WWALL  
      T(IJK)=TWALL
      ENDIF
   75 CONTINUE
C.....NORTH
      DO 85 I=2,NIM
      II=(I-1)*NK
      DO 85 K=2,NKM
      IK=II+K
      IJK=LK(K)+LI(I)+NJ
      IF(LBN(IK+IKST).EQ.1) THEN
      U(IJK)=UIN
      V(IJK)=VIN
      W(IJK)=WIN
      T(IJK)=TIN
      F2(IJK-1)=DENSIT*V(IJK)*(X(I+IST)-X(I+IST-1))
     *                       *(Z(K+KST)-Z(K+KST-1))
      XMOMIN=XMOMIN+F2(IJK-1)*V(IJK)
      FLOWIN=FLOWIN-F2(IJK-1)
      ELSEIF(LBN(IK+IKST).EQ.41) THEN
      U(IJK)=UWALL  
      V(IJK)=VWALL  
      W(IJK)=WWALL   
      T(IJK)=TWALL
      ENDIF
   85 CONTINUE
C.....WEST
      DO 95 J=2,NJM
      JJ=(J-1)*NK
      DO 95 K=2,NKM
      JK=JJ+K
      IJK=LK(K)+LI(1)+J
      IF(LBW(JK+JKST).EQ.1) THEN
      U(IJK)=UIN
      V(IJK)=VIN
      W(IJK)=WIN
      T(IJK)=TIN
      F1(IJK)=DENSIT*U(IJK)*(Z(K+KST)-Z(K+KST-1))*(Y(J+JST)-Y(J+JST-1))
      XMOMIN=XMOMIN+F1(IJK)*U(IJK)
      FLOWIN=FLOWIN+F1(IJK)
      ELSEIF(LBW(JK+JKST).EQ.41) THEN
      U(IJK)=UWALL   
      V(IJK)=VWALL   
      W(IJK)=WWALL   
      T(IJK)=TWALL
      ENDIF
   95 CONTINUE
C.....EAST
      DO 105 J=2,NJM
      JJ=(J-1)*NK
      DO 105 K=2,NKM
      JK=JJ+K
      IJK=LK(K)+LI(NI)+J
      IF(LBE(JK+JKST).EQ.1) THEN
      U(IJK)=UIN
      V(IJK)=VIN
      W(IJK)=WIN
      T(IJK)=TIN
      F1(IJK-NJ)=DENSIT*U(IJK)*(Z(K+KST)-Z(K+KST-1))
     *                        *(Y(J+JST)-Y(J+JST-1))
      XMOMIN=XMOMIN+F1(IJK-NJ)*U(IJK)
      FLOWIN=FLOWIN-F1(IJK-NJ)
      ELSEIF(LBE(JK+JKST).EQ.41) THEN
      U(IJK)=UWALL    
      V(IJK)=VWALL    
      W(IJK)=WWALL    
      T(IJK)=TWALL
      ENDIF
  105 CONTINUE
C.....SET NORMALIZATION FACTORS
C
      IF(FLOWIN.LT.SMALL) FLOWIN=1.
      IF(XMOMIN.LT.SMALL) XMOMIN=1.
      DO 120 I=1,NFI
      SNOR(I)=1.
  120 RESOR(I)=0.
      SNOR(IU)=1./XMOMIN
      SNOR(IV)=SNOR(IU)
      SNOR(IW)=SNOR(IU)
      SNOR(IP)=1./FLOWIN
      SNOR(IEN)=1.
      RETURN
C
C***********************************************************************
      ENTRY OUTPUT
C***********************************************************************
C
C.....PRINTING OF RESULTS
C
      IF(LPRI) THEN
      IF(LCAL(IU)) CALL PRINTI(U ,'U-VELOCITY',NI)
      IF(LCAL(IV)) CALL PRINTI(V ,'V-VELOCITY',NI)
      IF(LCAL(IW)) CALL PRINTI(W ,'W-VELOCITY',NI)
      IF(LCAL(IP)) CALL PRINTI(P ,' PRESSURE ',NI)
      IF(LCAL(IU)) CALL PRINTI(F1,' FLUX-1   ',NI)
      IF(LCAL(IU)) CALL PRINTI(F2,' FLUX-2   ',NI)
      IF(LCAL(IU)) CALL PRINTI(F3,' FLUX-3   ',NI)
      IF(LCAL(IEN)) CALL PRINTI(T,'TEMPERATUR',NI)
      ENDIF
      IF(LPRJ) THEN
      IF(LCAL(IU)) CALL PRINTJ(U ,'U-VELOCITY',NJ)
c     IF(LCAL(IV)) CALL PRINTJ(V ,'V-VELOCITY',NJ)
c     IF(LCAL(IW)) CALL PRINTJ(W ,'W-VELOCITY',NJ)
c     IF(LCAL(IP)) CALL PRINTJ(P ,' PRESSURE ',NJ)
c     IF(LCAL(IU)) CALL PRINTJ(F1,' FLUX-1   ',NJ)
c     IF(LCAL(IU)) CALL PRINTJ(F2,' FLUX-2   ',NJ)
c     IF(LCAL(IU)) CALL PRINTJ(F3,' FLUX-3   ',NJ)
      IF(LCAL(IEN)) CALL PRINTJ(T,'TEMPERATUR',NJ)
      ENDIF
      IF(LPRK) THEN
      IF(LCAL(IU)) CALL PRINTK(U ,'U-VELOCITY',NK)
      IF(LCAL(IV)) CALL PRINTK(V ,'V-VELOCITY',NK)
      IF(LCAL(IW)) CALL PRINTK(W ,'W-VELOCITY',NK)
      IF(LCAL(IP)) CALL PRINTK(P ,' PRESSURE ',NK)
      IF(LCAL(IU)) CALL PRINTK(F1,' FLUX-1   ',NK)
      IF(LCAL(IU)) CALL PRINTK(F2,' FLUX-2   ',NK)
      IF(LCAL(IU)) CALL PRINTK(F3,' FLUX-3   ',NK)
      IF(LCAL(IEN)) CALL PRINTK(T,'TEMPERATUR',NK)
      ENDIF
      RETURN
C
C***********************************************************************
      ENTRY MOOUT1
C***********************************************************************
      WRITE(6,661) TITLE,DENSIT,VISCOS,ALFA,SORMAX
  661 FORMAT(1H1,//,40X,A50,/,40X,50('='),//,50X,   
     *        'FLUID DENSITY    :  RHO = ',E10.4,/,50X,
     *        'DYNAMIC VISCOSITY:   MU = ',E10.4,/,50X,
     *        'ALFA  PARAMETER  : ALFA = ',F6.2 ,/,50X,
     *        'CONV. CRITERION  :  SOR = ',E10.4,/)
      IF(LCAL(IU))  WRITE(6,663) 'U',URF(IU),'U',GDS(IU),NSW(IU)
      IF(LCAL(IV))  WRITE(6,663) 'V',URF(IV),'V',GDS(IU),NSW(IV)
      IF(LCAL(IW))  WRITE(6,663) 'W',URF(IW),'W',GDS(IU),NSW(IW)
      IF(LCAL(IP))  WRITE(6,664) 'P',URF(IP),        NSW(IP)
      IF(LCAL(IEN)) WRITE(6,663) 'T',URF(IEN),'T',GDS(IEN),NSW(IEN)
  663 FORMAT(50X,'URF(',A1,')=',F5.2,'  GDS(',A1,')=',F5.2,'  NSW=',I3)
  664 FORMAT(50X,'URF(',A1,')=',F5.2,15X,' NSW=',I3)
      WRITE(6,*) '   '
      WRITE(6,*) '   '
      RETURN
C.....CALCULATE AND PRINT WALL SHEAR STRESSES
C***********************************************************************
      ENTRY MOOUT2
C***********************************************************************
c
c.....U at centerline
c
      call setind(kgrid)
      i=ni/2
      ii=i+ist
      j=nj/2
      zprof(1)=z(1+kst)
      do k=1,nk
        ijk=lk(k)+li(i)+j
        if(k.gt.1) zprof(k)=0.5*(z(k+kst)+z(k+kst-1))
        uprof(k)=(1.-fy(j+jst))*(u(ijk)*(1.-fx(ii))+u(ijk+nj)*fx(ii))+
     *         fy(j+jst)*(u(ijk+1)*(1.-fx(ii))+u(ijk+nj+1)*fx(i+ist))
      end do
      write(11,*) (uprof(k),k=1,nk)
      write(11,*) (zprof(k),k=1,nk)
c
c.....W at centerline
c
      j=nj/2
      k=nk/2
      kk=k+kst
      xprof(1)=x(1+ist)
      do i=1,ni
        ijk=lk(k)+li(i)+j
        if(i.gt.1) xprof(i)=0.5*(x(i+ist)+x(i+ist-1))
        wprof(i)=(1.-fy(j+jst))*(w(ijk)*(1.-fz(kk))+w(ijk+nij)*fz(kk))+
     *            fy(j+jst)*(w(ijk+1)*(1.-fz(kk))+w(ijk+nij+1)*fz(kk))
      end do
      write(12,*) (xprof(i),i=1,ni)
      write(12,*) (wprof(i),i=1,ni)
c
c.....pressure and shear force at bottom wall
c
      k=2
      fpy=0.
      ftx=0.
      do i=2,nim
      do j=2,njm
        ijk=lk(k)+li(i)+j
        sb=(x(i+ist)-x(i+ist-1))*(y(j+jst)-y(j+jst-1))
        dn=0.5*(z(2+kst)-z(1+kst))
        tau=vis(ijk)*u(ijk)/dn
        ftx=ftx+tau*sb
        fpy=fpy+p(ijk-nij)*sb
      end do
      end do
      write(6,*) ' Ftx = ',ftx,' ,   Fpy = ',fpy
c
c.....mass flux through the centerplane
c
      smp=0.
      smm=0.
      i=ni/2
      do k=2,nkm
      do j=2,njm
        ijk=lk(k)+li(i)+j 
        if(f1(ijk).gt.0.) smp=smp+f1(ijk)
        if(f1(ijk).lt.0.) smm=smm+f1(ijk)
      end do
      end do
      write(6,*) ' Sum of positive mass fluxes:  ',smp
      write(6,*) ' Sum of negative mass fluxes:  ',smm  
c
      RETURN
      END
